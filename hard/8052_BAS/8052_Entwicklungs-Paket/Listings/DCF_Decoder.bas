1      REM        8052-BASIC DCF-CLOCK
2      REM
3      REM        VARIABLEN UND STRINGS
4      REM
5      REM AO      = DISPLAY ADRESS OFFSET FOR TIME
6      REM BI      = "1" IF PULSE .1 S, "0" IF PULSE .2 S
7      REM BT      = TIME BETWEEN TWO EXT. INTERRUPTS
8      REM BV      = BIT FLAG, "1" IF BIT RECEIVED
9      REM CHAR    = CHARCTER FROM SERIAL INPUT
10     REM D0 - D6 = DISPLAY INFO
11     REM DAV     = DATE FLAG, "1" IF DATE TO DISPLAY
12     REM I       = FOR NEXT LOOP
13     REM I0 - I6 = NEW RECEIVED BITS
14     REM IN      = INTERFERENCE TIME
15     REM ITV     = ONTIME FLAG, "1" AFTER ONTIME INTERRUPT
16     REM K1...K5 = RECEIVED BITS
17     REM KM      = KMAX = 2^16-1
18     REM LE      = TIME OF LAST EXT. INTERRUPTS
19     REM LS1     = LAST SYNC TIME
20     REM LS2     = LAST SYNC TIME
21     REM LSV     = LAST SYNC. FLAG
22     REM MNVO    = DATE ON DISPLAY
23     REM MU      = MULTIPLIER NEEDED FOR BIT SHIFT
24     REM N       = DISPLAY COMMAND OR TYPE
25     REM O1 - O6 = OLD RECEIVED TIME AND DATE
26     REM OFS     = TIME OFFSET
27     REM OKV     = OK FLAG, "1" IF TWO RECEIVED MINITS ARE OK
28     REM PA      = PARITY COUNT
29     REM PAR     = PARITY
30     REM PC      = INPUT PULSE COUNT
31     REM PR      = DISPLAY LINE POINTER, 1=UPPER, 2=LOWER
32     REM PW      = PULSE WIDTH OF INCOMING PULSE ON EXT1
33     REM Q       = TEMPORARY INTEGER PORTION OF TIME
34     REM RV      = RETI FLAG
35     REM SC      = DISPLAY SYNC. CHARACTER
36     REM SO      = SYNC. CHARACTER ADRESS OFFSET
37     REM SUTV    = SUMMER TIME FLAG, "1" IF SUMMER TIME
38     REM SYV     = SYNCHRONISE FLAG, "1" IF SYNCHR.
39     REM T0 - T6 = TIME IN SUBROUTINES INCR./DECR.-TIME
40     REM T       = TEMPORARY TIME
41     REM TC      = TIMER1 CONTENTS FOR PULSEWIDTH MEASUREMENT
42     REM TD      = DISPLAY TIME FOR LAST SYNC. TIME
43     REM TI      = TEMPORARY IN. TIME
44     REM TIME    = TEMPORARY TIME
45     REM X       = FOR NEXT LOOP
46     REM XTAL    = SYSTEMFREQUENCE
47     REM
48     REM $(0)          =
49     REM $(1) - $(7)   = WEEKDAY LOOKUP TABLE
50     REM $(11) - $(22) = MONTH LOOKUP TABLE
51     REM $(73)         =
52     REM
100   XBY(0C003H)=255
110   XTAL=12000000
120    CLOCK 0 : TIME=0
130    STRING 2000,16
140   $(1)="Mon." : $(2)="Die." : $(3)="Mit." : $(4)="Don."
150   $(5)="Fre." : $(6)="Sam." : $(7)="Son."
160   $(11)="Jan." : $(12)="Feb." : $(13)="Mrz." : $(14)="Apr."
170   $(15)="Mai " : $(16)="Juni" : $(17)="Juli" : $(18)="Aug."
180   $(19)="Sep." : $(20)="Okt." : $(21)="Nov." : $(22)="Dez."
210   $(73)="     "
240   D0=0 : D1=0 : D2=0 : D3=1 : D4=1 : D5=1 : D6=0
250   O1=0 : O2=0 : O3=1 : O4=1 : O5=1 : O6=0
270   KM=65535
280   DAV=1 : SUTV=2 : SC=45 : PC=59 : MU=1 : IN=1
290    PRINT USING(##), :  REM SET PRINT FORMAT
300   N=56 :  GOSUB 5000 : N=12 :  GOSUB 5000 : N=1 :  GOSUB 5000
318   RCAP2=65496 :  REM 9600 BAUD
330   MNVO=10
350   TCON=244 : TIMER1=0
360    ONEX1 3940
370   DBY(71)=0 :  CLOCK 1
380   LE=TIME
390    ONTIME 1,4280
406    GOSUB 3080
420    REM ------------------ MAIN PROGRAMM -----------------------
430    DO 
440    IF BV=1 GOSUB 570 :  REM INPUT NEW BIT
490    IF ITV=1 GOSUB 970 :  REM INCR. TIME AND SER.I
500    IF SYV=1 GOSUB 1330 :  REM SYNCHRONISE
510    IF RV=1.AND.ITV=0 GOSUB 4340 :  REM ENABLE ONEX1 AGAIN
520    IF DAV=1.AND.LSV=0 GOSUB 2260 :  REM date to display
530    UNTIL 0
560    REM NEW BIT ENTRY AFTER ONEX1 INTERRUPT SERVICE ( BV=1 )
570   BV=0
580   PW=TI-TC*.0016
590    IF PW>.15 THEN BI=1 ELSE BI=0 :  REM NEW BIT
600    REM BITS TO REGISTER K1...K5
610    REM ATTENTION: SHIFTED-IN BIT BELONGS WITH PC-1
620    REM I.E. PC PACES AHEAD OF BIT
630   MU=MU*2 : PA=PA+BI
632    IF PC=21 THEN PA=0 : PAR=0
634    IF PC=29 THEN PAR=PAR.OR.PA : PA=0
636    IF PC=36 THEN PAR=PAR.OR.PA : PA=0
640    IF PC=1.OR.PC=16.OR.PC=22.OR.PC=37.OR.PC=46 THEN MU=1
650    IF PC>45 THEN K5=K5+BI*MU :  GOTO 700
660    IF PC>36 THEN K4=K4+BI*MU :  GOTO 700
670    IF PC>21 THEN K3=K3+BI*MU :  GOTO 700
680    IF PC>15 THEN K2=K2+BI*MU :  GOTO 700
690    IF PC<16 THEN K1=K1+BI*MU
700   OKV=0
710    IF PC<>58 RETURN 
720    REM DECODE NEW INFO
730    IF K1>KM.OR.K2>KM.OR.K3>KM.OR.K4>KM.OR.K5>KM GOTO 820
740   I1=(K3.AND.15)+10*(K3.AND.112)/16
750   I2=(K3.AND.3840)/256+10*(K3.AND.12288)/4096
760   I3=(K4.AND.15)+10*(K4.AND.48)/16
770   I4=(K4.AND.448)/64
780   I5=(K5.AND.15)+10*(K5.AND.16)/16
790   I6=(K5.AND.480)/32+10*(K5.AND.7680)/512
800   I0=(K2.AND.12)/4 :  REM SUMMER TIME INFO
810    REM OLD INFO (O) +1MIN. EQUAL NEW INFO (I)?
820   OKV=0 : T0=59 : T1=O1 : T2=O2 : T3=O3 : T4=O4 : T5=O5 : T6=O6
830    GOSUB 1760
840    IF O0=I0.AND.T1=I1.AND.T2=I2.AND.T3=I3.AND.T4=I4 THEN OKV=1
850    IF NOT(OKV=1.AND.T5=I5.AND.T6=I6) THEN OKV=0
860    REM OLD INFO = NEW INFO
870   O0=I0
880   O1=I1 : O2=I2 : O3=I3 : O4=I4 : O5=I5 : O6=I6
890   K1=0 : K2=0 : K3=0 : K4=0 : K5=0
900   MU=1
910    RETURN 
970   ITV=0
1030   GOSUB 2100 :  REM TIME TO DISPLAY
1185   IF D0=0.AND.D1=0.AND.D2=0 THEN DAV=1
1190   REM READ CHARACTER FROM SERIAL INPUT
1200  CHAR=GET
1210   IF CHAR=0 GOTO 1260
1220   IF CHAR=84 GOSUB 2660 :  REM (84="T") TIME TO SERIAL OUTPUT
1230   IF CHAR=36 GOSUB 2730 :  REM (36="$") TIME STRING TO SER. OUTPUT
1240   IF CHAR=76 GOSUB 2800 :  REM (76="L") LAST SYNC. TIME TO SER. OUTPUT
1250   IF CHAR=83 GOSUB 2860 :  REM (83="S") SET TIME
1260   REM
1270   GOSUB 1670 :  REM INCREMENT TIME
1280   IF TD<3 THEN TD=TD+1 ELSE TD=0
1290   RETURN 
1320   REM Synchronise   ( one second after new minute  )
1325   REM if d0=0 and po=0 then extra sec.pulse
1330  SYV=0 : SUTV=I0 : LS1=I1 : LS2=I2
1340   GOTO 1400 :  REM dcf-time on display
1350   IF SUTV=1 THEN OFS=-2 ELSE OFS=-1
1370  T1=I1 : T2=I2 : T3=I3 : T4=I4 : T5=I5 : T6=I6
1380   GOSUB 1950 :  REM decrement received time
1390  I1=T1 : I2=T2 : I3=T3 : I4=T4 : I5=T5 : I6=T6
1400  DAV=1 : SC=42 : D0=1 : D1=I1 : D2=I2 : D3=I3 : D4=I4 : D5=I5 : D6=I6
1410   RETURN 
1660   REM increment display time
1670  T0=D0 : T1=D1 : T2=D2 : T3=D3 : T4=D4 : T5=D5 : T6=D6
1680   GOSUB 1760
1690  D0=T0 : D1=T1 : D2=T2 : D3=T3 : D4=T4 : D5=T5 : D6=T6
1700   IF D0=1 THEN SC=45 :  REM clear display sync. char
1710   RETURN 
1740   REM subroutine increment time one second (year<2100)
1750   REM t0=sec, t1=min, t2=hour, t3=day, t4=wday, t5=month, t6=year
1760  T0=T0+1
1770   IF T0>59 THEN T0=0 : T1=T1+1
1780   IF T1>59 THEN T1=0 : T2=T2+1
1790   IF T2<24 THEN  RETURN 
1810  T2=0 : T4=T4+1
1820   IF T4>7 THEN T4=1
1830  T3=T3+1
1840   IF T3>29.AND.T5=2 THEN T3=1 : T5=T5+1
1850   IF T3=29.AND.T5=2.AND.(4*INT(T6/4)<>T6) THEN T3=1 : T5=T5+1
1860   IF T3=31.AND.(T5=4.OR.T5=6.OR.T5=9.OR.T5=11) THEN T3=1 : T5=T5+1
1870   IF T3>31 THEN T3=1 : T5=T5+1
1880   IF T5>12 THEN T5=1 : T6=T6+1
1890   IF T6>99 THEN T6=0
1900   RETURN 
1930   REM subroutine decrement time one hour
1940   REM t0=sec, t1=min, t2=hour, t3=day, t4=wday, t5=month, t6=year
1950  T2=T2+OFS
1960   IF T2>=0 RETURN 
1970  T2=T2+24
1980  T4=T4-1 :  IF T4<1 THEN T4=7
1990  T3=T3-1
2000   IF T3<1.AND.T5=3.AND.(4*INT(T6/4)=T6) THEN T3=29 : T5=T5-1
2010   IF T3<1.AND.T5=3 THEN T3=28 : T5=T5-1
2020   IF T3<1.AND.(T5=5.OR.T5=7.OR.T5=10.OR.T5=12) THEN T3=30 : T5=T5-1
2030   IF T3<1 THEN T3=31 : T5=T5-1
2040   IF T5<1 THEN T5=12 : T6=T6-1
2050   IF T6<0 THEN T6=99
2060   RETURN 
2090   REM time to display (d0...d2)
2100   REM
2105  N=138+AO :  GOSUB 5000
2110  N=48+D0/10 :  GOSUB 5100
2120  N=48+D0-10*INT(D0/10) :  GOSUB 5100
2130   IF MNVO=10.AND.D0>1 RETURN  :  REM return as soon as possible
2140  N=135+AO :  GOSUB 5000
2150  N=48+D1/10 :  GOSUB 5100
2160  N=48+D1-10*INT(D1/10) :  GOSUB 5100
2165  N=58 :  GOSUB 5100
2170  N=132+AO :  GOSUB 5000
2180   IF D2<10 THEN N=32 ELSE N=48+D2/10
2182   GOSUB 5100
2190  N=48+D2-10*INT(D2/10) :  GOSUB 5100
2195  N=58 :  GOSUB 5100
2200  N=128+SO :  GOSUB 5000
2210  N=SC :  GOSUB 5100
2220   RETURN 
2250   REM date to display (d3...d6)
2260  DAV=0
2270  N=192 :  GOSUB 5000
2280   FOR X=1 TO 4
2290  N=ASC($(D4),X) :  GOSUB 5100
2300   NEXT X
2310  N=32 :  GOSUB 5100
2320   IF D3<10 THEN N=32 ELSE N=48+D3/10
2322   GOSUB 5100
2330  N=48+D3-10*INT(D3/10) :  GOSUB 5100
2340  N=ASC(.) :  GOSUB 5100
2345  N=32 :  GOSUB 5100
2350   FOR X=1 TO 4
2360  N=ASC($(D5+10),X) :  GOSUB 5100
2370   NEXT X
2380  N=39 :  GOSUB 5100
2390  N=48+D6/10 :  GOSUB 5100
2400  N=48+D6-10*INT(D6/10) :  GOSUB 5100
2410   RETURN 
2440   REM output one line to display
2450   REM if pr=1 then $(0) to upper else to lower
2470   REM $(0)=string to be displayed
2475   IF PR=1 THEN N=128 ELSE N=192
2477   GOSUB 5000
2480   FOR I=1 TO 16
2482  N=ASC($(0),I) :  GOSUB 5100
2484   NEXT I
2490   RETURN 
2640   REM serial output routine (char=t)
2650   REM stray instructions to preclude halting onex1 interrupt
2660   PRINT D4, :  PRINT D3, :  PRINT D5, :  PRINT D6,
2665   PRINT D2, :  PRINT D1, :  PRINT D0,
2670   PRINT SC, :  PRINT SUTV
2680   RETURN 
2710   REM serial output routine (char=$)
2720   REM stray instructions to preclude halting onex1 interrupt
2730   PRINT $(D4), :  PRINT D3, :  PRINT ". ", :  PRINT $(10+D5),
2740   PRINT D6, :  PRINT D2, :  PRINT ":", :  PRINT D1,
2750   PRINT ":", :  PRINT D0, :  PRINT " ", :  PRINT CHR(SC)
2760   RETURN 
2790   REM serial output routine (char=l)
2800   PRINT "Letz", :  PRINT "ter ", :  PRINT "kor. ", :  PRINT "Empf",
2802   PRINT "ang ", :  PRINT LS2, :  PRINT ":", :  PRINT LS1
2810   RETURN 
2840   REM set time via serial input (char=s)
2860   INPUT "wday,day,month,year,hr,min,sec"D4,D3,D5,D6,D2,D1,D0
2870   IF D2>23.OR.D1>59.OR.D0>59.OR.D4>7.OR.D3>31.OR.D5>12 GOTO 2860
2880   IF D2<0.OR.D1<0.OR.D0<0.OR.D4<1.OR.D3<1.OR.D5<1.OR.D6<0 GOTO 2860
2890  D6=D6-100*(INT(D6/100)) :  REM delete centuries
2895  SC=45
2900  MNVO=0 :  GOSUB 2100 : MNVO=10 :  REM DATE AND TIME ON DISPLAY
2910  DAV=1
2920   RETURN 
3080  $(0)="DCF       :  :  "
3104   FOR X=1 TO 5 : ASC($(73),X)=ASC($(70+ME(4)),X) :  NEXT 
3105  AO=4 : SO=6 : PR=1 :  GOSUB 2470 :  GOSUB 2100
3106   RETURN 
3107  $(0)="      :  :      "
3108  AO=0 : SO=0 : PR=1 :  GOSUB 2470 :  GOSUB 2100
3109   RETURN 
3900   REM Unterprogramm Onex1-Routine
3940  T=TIME : BT=T-LE :  IF BT<.17 RETI 
3945  TMOD=240 :  REM stop timer1
3950   IF BT<.95 GOTO 4170
3960  TC=TIMER1
3970   IF BT<1.05 GOTO 4120
3980   IF BT<1.95.OR.BT>2.05 GOTO 4230
3992   IF IN=1 THEN IN=2
3994   IF IN-TC*.0016>.15 THEN PA=PA+1
3996  PAR=(PAR.OR.PA).AND.1
3998   IF PAR=1 THEN OKV=0 :  REM parity check
4000   IF OKV=0 GOTO 4080
4010   CLOCK 0 : Q=TIME-T+.15 : TIME=0 :  REM ontime is 0.15s ahead
4020  DBY(71)=200*Q :  REM set integer portion of time
4030   CLOCK 1 :  ONTIME 1,4280
4040  T=.15-.015 : SYV=1 : ITV=0 : OKV=0
4070   REM label1
4080  PC=59 : K1=0 : K2=0 : K3=0 : K4=0 : K5=0
4090  MU=1
4110   REM label2
4120  TIMER1=0 : TI=IN : IN=1 : LE=T : BV=1 : TMOD=208
4130   IF PC<59 THEN PC=PC+1 ELSE PC=0 : K1=0 : K2=0 : K3=0 : K4=0 : K5=0
4131   IF PC=0 THEN MU=1
4140   RETI 
4160   REM label3
4170   IF IN=1 THEN IN=BT+.01 :  RETI  :  REM 10 ms offset
4190   IF BT<.5.AND.ITV=1 THEN RV=1 :  RETURN  :  REM disable ext1!!!!
4200   RETI 
4220   REM label4
4230   IF BT>2 GOTO 4080 ELSE  IF PC<>58 GOTO 4080
4233   IF IN=1 THEN IN=BT+.01 :  RETI  :  REM 10 ms offset
4235   IF BT<1.5.AND.ITV=1 THEN RV=1 :  RETURN  :  REM disable ext1!!!!
4240   RETI 
4270   REM ontime interrupt service routine
4280  ITV=1 :  REM set ontime int. flag
4290   ONTIME TIME+1,4280
4300   RETI 
4330   REM enable onex1 int. again !
4340  RV=0
4350   RETI 
5000   REM ----Kommando-----
5010  XBY(0C000H)=N
5020  XBY(0C001H)=192
5030  XBY(0C001H)=128
5040  XBY(0C001H)=64
5050  XBY(0C001H)=192
5060   RETURN 
5100   REM -----Daten------
5110  XBY(0C000H)=N
5120  XBY(0C001H)=64
5130  XBY(0C001H)=192
5140   RETURN 

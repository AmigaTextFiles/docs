@DATABASE "ADB71"
@INDEX "ADB:Misc/Index.Text/Main"
@HELP "ADB:Misc/Help/Main"
@NODE "Main" "AmigaDOS For Beginners - Part 71 - Startup-Sequence Files"

@{B}@{U}@{JCENTER}AMIGADOS FOR BEGINNERS

BY FRANK BUNTON

@{" COPYRIGHT © FRANK P. BUNTON 1993-1998 " LINK "ADB:Misc/Read_Me_First!!/Copyright"}

PART 71 - THE STARTUP-SEQUENCE FILES EXPLAINED@{UB}@{UU}


@{" General Comments On Startup Files " LINK "General"} All AmigaDOS Versions
@{" The V1.2 Startup-Sequence File    " LINK "1.2"} Version 1.2 Only     
@{" The V1.3 Startup-Sequence File    " LINK "1.3"} Version 1.3 Only     
@{" The V1.3 StartupII File           " LINK "1.3II"} Version 1.3 Only     
@{" The V2.0 Startup-Sequence File    " LINK "2.0"} Version 2.0 Only     
@{" The V2.1 Startup-Sequence File    " LINK "2.1"} Version 2.1 Only     
@{" The V3.1 Startup-Sequence File    " LINK "3.1"} Version 3 Only       


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "General" "Part 71 - Startup-Sequence Files - General"

@{B}@{U}General Comments On The Startup-Sequence Files@{UB}@{UU}

When booting a disk, not only does it need to have a boot block @{"installed" LINK "ADB:Parts_11-20/17.Install/Main"},
but also the system needs to find a file called "Startup-Sequence" in
the boot disk's "S" directory. Without this file, the system will stop
with a Shell or CLI window.

In @{B}Version 1.3 and earlier@{UB} this window will be a simple CLI without any
of the Shell enhancements.

In @{B}Version 2.0 onwards@{UB} this window will be a Shell window but without
the extras provided by the "Shell-Startup" file.

The Startup-Sequence file need only have one command. This may be "LOADWB"
or the name of a program on the disk.

The Startup-Sequence file can also become quite complex, carrying out
all sorts of jobs, and starting up numerous background tasks. In other
words, the user can have as much or as little customisation of the file
as he/she likes.

Over the years, there has been a considerable evolution in the standard
startup-sequence files on the Workbench disks. However, @{B}all versions@{UB} of
AmigaDOS have at least one startup file which is called @{B}"Startup-Sequence"@{UB}.

@{B}In version V1.3 @{UB} a second startup script file came into being. It was
called:-

  StartupII

This file is on the standard Workbench 1.3 disk and is called from the
startup-sequence file with the line:-

  RUN EXECUTE s:StartupII

The reason for this file is explained in the @{"RESIDENT" LINK "ADB:Parts_21-30/24.Resident/1.3"} article.

@{B}From version 2.0 onwards@{UB} there is the ability to call a second startup
script with these lines:-

  IF EXISTS S:User-Startup
    EXECUTE S:User-Startup
  ENDIF

This extra "User-Startup" file is @{B}not@{UB} on the standard workbench disks.
It is only created by users who wish to add to the startup sequence without
changing the standard "Startup-Sequence" file.

You will find that many installation scripts will automatically add items
to the "User-Startup" file to ensure that the program that is being
installed will work properly. For instance, assignments to the program
directory or additions to the Libs: assignment may be made.

Sometimes the contents of these files are a bit obscure to the beginner
so I will go through them.

A lot of the lines in these files will need no explanation. (At least I
hope not! If you have got this far and cannot understand a line that I do
not explain then maybe my articles are hopelessly inadequate! Or you have
not read them properly!)

The numbers in front of each line are not, of course, a part of the file.
They are there purely for reference in these notes.

Rather than showing you the file as a whole, I will only display each line
as I discuss it. If you wish to view your own startup files then click on
one of the following:-

  @{" Startup-Sequence File " LINK "S:Startup-Sequence/MAIN"} All AmigaDOS Versions
  @{" StartupII File        " LINK "S:StartupII/MAIN"} Version 1.3 Only
  @{" User-Startup File     " LINK "S:User-Startup/MAIN"} Version 2.0 Onwards

If you click on @{B}"StartupII File"@{UB} or @{B}"User-Startup File"@{UB} and get:-

- a message in this windows title bar saying:-

     Couldn't open ...... (filename)

  then your system does not have that particular file.

- an empty screen but showing the name of the file in the title bar
  then the file is there but it has no content. I have seen systems where
  all entries in, say, the "User-Startup" file, have been deleted but the
  file name remains in the S: directory.

If you click on @{B}"Startup-Sequence File"@{UB} and get a situation similar to one
of the above two then you have done the seemingly impossible and got this
far without a "Startup-Sequence" file on your boot disk!

The following references explain the various startup files:-

  @{" The V1.2 Startup-Sequence File " LINK "1.2"} Version 1.2 Only
  @{" The V1.3 Startup-Sequence File " LINK "1.3"} Version 1.3 Only
  @{" The V1.3 StartupII File        " LINK "1.3II"} Version 1.3 Only
  @{" The V2.0 Startup-Sequence File " LINK "2.0"} Version 2.0 Only
  @{" The V2.1 Startup-Sequence File " LINK "2.1"} Version 2.1 Only
  @{" The V3.1 Startup-Sequence File " LINK "3.1"} Version 3 Only


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "1.2" "Part 71 - Startup-Sequence Files - The 1.2 Startup-Sequence File"

@{B}@{U}The V1.2 Startup-Sequence File@{UB}@{UU}

@{B} 1. ECHO "A500 Workbench 1.2 Version 33.56 23-APR-87*N"
 2. BINDDRIVERS

 3. IF EXISTS Sys:System
 4.   PATH Sys:System ADD
 5. ENDIF
 6. IF EXISTS Sys:Utilities
 7.   PATH Sys:Utilities ADD
 8. ENDIF

Lines 3 - 8@{UB}. These lines add the specified directory to the path if the
directory actually exists. If the particular directory does not exist
then the PATH command is ignored.

The PATH command allows multiple directories to be included in the same
command line. For example:-

   PATH >Nil: Sys:System Sys:Utilities ADD

With this command line, if the directory does not exist, the PATH gives
a message saying that the directory can't be found, but this message is
sent to Nil: so you don't see it.

It then goes on to the next directory in the command line and adds it
if it exists.

Later versions of PATH (v2.0 and later) will stop after finding that a
directory does not exist and thus directories after the nonexistent one
will not be added.

@{B} 9. DIR Ram:

Line 9@{UB} is there because the Ram: drive does not become active until some
command makes reference to it. However, this is done by Line 10 so I cannot
see any reason for Line 9!

@{B}10. PATH Ram: ADD

@{B}11. SETMAP usa1

Line 11@{UB}. The older Amigas (including the A500) had an inbuilt keymap that
did not recognise the ()/* keys on the numeric key pad. (Was this a hang
over from an earlier Amiga?!?!). The @{"SETMAP" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/SetMapKey"} command overrides the inbuilt
keymap with the "usa1" map which does recognise these keys. Any keymap
can be used here.

@{B}12. ADDBUFFERS df0: 20
13. LOADWB

14. FAILAT 30
15. SETCLOCK >Nil: Opt LOAD

Lines 14-15@{UB}. The FAILAT command is there in case the Amiga in use does
not have a battery backed up @{"Real Time Clock" LINK "ADB:Parts_41-50/44.Clock_Commands/Real"}. If it does not have one,
then the SETCLOCK command would fail.

@{B}16. DATE
17. ENDCLI > Nil:@{UB}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "1.3" "Part 71 - Startup-Sequence Files - The 1.3 Startup-Sequence File"

@{B}@{U}The V1.3 Startup-Sequence File@{UB}@{UU}

@{B} 1. ADDBUFFERS df0: 10
 2. C:SETPATCH >Nil: ;patch system functions

 3. CD C:

Line 3@{UB}. Presumably, commands will be loaded slightly faster if they are
in the current directory.

@{B} 4. ECHO "A500/A2000 Workbench disk. Release 1.3 version 34.20*N"

 5. Sys:System/FASTMEMFIRST ; move C00000 memory to last in list

Line 5@{UB}. This is for those people who have added Fast RAM to the basic
0.5mb @{B}v1.x@{UB} Amiga that is all Chip RAM. It ensures that programs are loaded
into Fast RAM first (if there is room) instead of into Chip RAM.

@{B} 6. BINDDRIVERS
 7. SETCLOCK LOAD ;load system time from real time clock (A1000 owners
    ; should replace the SETCLOCK LOAD with DATE

 8. FF >Nil: -0

Line 8@{UB}. FF is a program in @{B}v1.x@{UB} that speeds up fonts usage. "-0" (zero)
turns it on and "-N" turns it off.

@{B} 9. RESIDENT CLI L:Shell-Seg SYSTEM PURE ADD; activate Shell

Line 9@{UB}. This is a bit complex but it was explained in the @{"RESIDENT" LINK "ADB:Parts_21-30/24.Resident/1.3"} article.

@{B}10. RESIDENT C:Execute PURE
11. MOUNT Newcon:
12. FAILAT 11

13. RUN EXECUTE s:StartupII ;This lets RESIDENT be used for rest of script

Line 13@{UB}. The StartupII file was introduced so as to enable the use of
resident commands during startup. The "RUN" command before "EXECUTE" ensures
that this can be done. This is also explained in the @{"RESIDENT" LINK "ADB:Parts_21-30/24.Resident/1.3"36} article.

@{B}14. WAIT >Nil: 5 mins

Line 14@{UB}. This waits until a break signal is received from StartupII to
indicate that it has finished (See @{"Line 22 of StartupII" LINK "1.3II"57}). This BREAK stops
the WAIT command from waiting its full 5 minutes.

@{B}15. Sys:System/SETMAP usa1 ;Activate the ()/* on keypad

Line 15@{UB}. The older Amigas (including the A500) had an inbuilt keymap that
did not recognise the ()/* keys on the numeric key pad. (Was this a hang
over from an earlier Amiga?!?!). The @{"SETMAP" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/SetMapKey"} command overrides the inbuilt
keymap with the "usa1" map which does recognise these keys. Any keymap
can be used here.

@{B}16. PATH Ram: C: Sys:Utilities Sys:System s: Sys:Prefs ADD

Line 16@{UB} assumes that all the directories mentioned in the command line
are on the boot disk. If one of them is not, then the v1.3 PATH command
will continue on to add the rest of the directories in the command line.

@{B}17. LOADWB DELAY ;wait for inhibit to end before continuing
18. ENDCLI >Nil:@{UB}


@{JCENTER}=== End of Text ===
@{JLEFT}

























@ENDNODE

@NODE "1.3II" "Part 71 - Startup-Sequence Files - The 1.3 StartupII File"

@{B}@{U}The V1.3 StartupII File@{UB}@{UU}

@{B} 1. RESIDENT C:Resident PURE
 2. RESIDENT C:List PURE ;pre-load List and Cd
 3. RESIDENT C:Cd PURE
 4. RESIDENT C:Mount PURE ;the next 3 are loaded for speed during startup
 5. RESIDENT C:Assign PURE
 6. RESIDENT C:Makedir PURE

Lines 1 and 4, 5 and 6@{UB} are there to make these commands work faster during
startup.

@{B}Lines 2 and 3@{UB} appear to be there in case they are needed during normal
working after startup. They could be taken out, replaced with or added
to with other commands as suggested in @{B}Lines 7, 8 and 9@{UB}.

@{B} 7. ;make IF, ENDIF, ELSE, SKIP, ENDSKIP, and ECHO resident if
 8. ;you use scripts much, and can afford the ram.
 9. ;also make FAILAT, WAIT, and ENDCLI resident if you use IconX a lot

10. MAKEDIR Ram:T
11. ASSIGN T: Ram:T ;set up T: directory for scripts

Lines 10 and 11@{UB} set up the assigned directory T: in Ram:. The @{B}v1.3@{UB} "EXECUTE"
command makes use of T: if it exists. If T: does not exist (as in @{B}v1.2@{UB})
then "EXECUTE" will make a "T" directory in the current disk's root
directory (if it does not already exists) and that is used.

@{B}12. MAKEDIR Ram:Env ; set up Env: directory
13. ASSIGN Env: Ram:Env

Lines 12 and 13@{UB} make available the assigned Env: directory for environmental
variables that were introduced in @{B}v1.3.

14. MAKEDIR Ram:Clipboards ;set up CLIPS: assign
15. ASSIGN CLIPS: Ram:Clipboards

Lines 14 and 15@{UB} makes available the assigned directory CLIPS:, another
introduction in @{B}v1.3@{UB} . This is used by the Clipboard.device to store clips.
If Clips: does not exists (as in@{B} v1.2@{UB}) then, when clips are used by a
program, the directory "Devs:Clipboards" is created (if it does not exists)
and used for clips.

@{B}16. MOUNT Speak: ;just mounting doesn't take much ram at all
17. MOUNT Aux:
18. MOUNT Pipe:

Lines 16, 17 and 18@{UB} can be left out if you are short of memory and don't
use these devices.

@{B}19. RESIDENT Mount REMOVE ;if you have enough ram, keep these resident
20. RESIDENT Assign REMOVE ;by removing these lines
21. RESIDENT Makedir REMOVE

22. BREAK 1 C ;signal to other process its OK to finish

Line 22@{UB} signals to Startup-Sequence that StartupII has finished. The "BREAK
1 C" goes to the "WAIT" command which is the command currently in operation
in process 1. WAIT now does not last the full 5 minutes specified in @{"Line 14" LINK "1.3"42}
of Startup-Sequence.


@{JCENTER}=== End of Text ===
@{JLEFT}


































@ENDNODE

@NODE "2.0" "Part 71 - Startup-Sequence Files - The 2.0 Startup-Sequence File"

@{B}@{U}The V2.0 Startup-Sequence File@{UB}@{UU}

@{B} 1. C:SETPATCH >Nil:

 2. C:VERSION >Nil:

Line 2@{UB} checks to ensure that the disk is being booted on an Amiga with
kickstart @{B}v2.04 or higher@{UB}. The VERSION command also sets the shell's local
variables $Workbench and $Kickstart which are used in @{"Lines 18-19" LINK "2.0"73}. (See
article @{"VERSION" LINK "ADB:Parts_51-60/54.Version/Finding"23}.)

@{B} 3. ADDBUFFERS >Nil: df0: 15
 4. FAILAT 21

 5. RESIDENT >Nil: C:Execute PURE ADD

 6. MAKEDIR Ram:T Ram:Clipboards Ram:Env Ram:Env/Sys

Line 6@{UB} sets up all the RAM: directories that the system will use.

@{B} 7. COPY >Nil: Envarc: Ram:Env ALL QUIET NOREQ 

Line 7@{UB} was a new addition to startup-sequence files from @{B}v2.0 onwards@{UB}.
It copies the contents of Envarc: (which is automatically assigned to
Sys:Prefs/Envarc on boot up) to Ram:Env to allow faster access to your
personal preference settings which are held in Sys:Prefs/Envarc. Programs
will look in Ram:Env for preference settings. If you save new settings
from a preferences program (e.g. printer) then the new setting will be
saved to both Sys:Prefs/Envarc AND Ram:Env.

@{B} 8. ASSIGN Env: Ram:Env

Line 8@{UB} makes available the assigned Env: directory for environmental
variables that were first introduced in @{B}v1.3.@{UB} (Ram:Env is created in
Line 6.)

@{B} 9. ASSIGN T: Ram:T ;set up T: directory for scripts

Line 9@{UB} sets up the assigned T: directory for the use of scripts. (Ram:T is
created in Line 6.)

@{B}10. ASSIGN CLIPS: Ram:Clipboards

Lines 10@{UB} makes available the assigned directory CLIPS:. This is used by
the Clipboard.device to store clips. (Ram:Clipboards is created in Line 6.)

@{B}11. ASSIGN Rexx: S:

Line 11@{UB} allows use of an assigned directory Rexx: for use with ARexx scripts.
You can change this to another directory if you like, For example, you
could create a directory called Sys:ARexxS and assign Rexx: to this if
you do not want your ARexx scripts to be mixed in with other scripts.

@{B}12. IF EXISTS Sys:Monitors
13.   JOIN >Nil: Sys:Monitors/~(#?.info) as T:mon-start
14.   EXECUTE T:mon-start
15.   DELETE >Nil: T:mon-start
16. ENDIF

Lines 12-16@{UB}. If you have a monitors drawer on your boot disk containing
one or more scripts that are used to add a monitor, then this will join all
the scripts into the one script file called "T:mon-start" and execute it.
In @{B}v2.0@{UB} these scripts look like this:-

   Sys:System/ADDMONITOR NUM=2 NAME=pal.monitor

It would be unusual to have more than one monitor script in the drawer.
However, if there is more than one then there would be two or more lines
like this in the "T:Mon-Start" file.

@{B}17. BINDDRIVERS

18. SETENV Workbench $Workbench
19. SETENV Kickstart $Kickstart

Lines 18-19@{UB} set the global environmental variables Workbench and Kickstart
from the shell's local variables $Workbench and $Kickstart. These contain
the version numbers and thus they are made global for use by any program.

In @{B}v2.0@{UB} the local variables are left in place whereas in @{"v2.1" LINK "2.1"120} they are
removed.

@{B}20. IPREFS

Line 20@{UB}. @{"IPREFS" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/IPrefs"}  is a program that reads the individual preference settings
from the files stored in Ram:Env/Sys (copied there from Sys:Prefs/Envarc
in Line 7) and passes them to Workbench.

@{B}21. ECHO "Amiga Release 2. Kickstart $Kickstart, Workbench $Workbench"

Line 21@{UB} displays the version numbers on screen.

@{B}22. CONCLIP

Line 22@{UB}. @{"CONCLIP" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/ConClip"} is a program introduced in @{B}v2.0@{UB} that allows text to be
copied and pasted from one console window to another or to and from any
application that supports reading and writing text with the
clipboard.device.

@{B}23. MOUNT Speak:
24. MOUNT Aux:
25. MOUNT Pipe:

26. PATH Ram: C: Sys:Utilities Sys:rexxc Sys:System s: Sys:Prefs
         Sys:wbstartup ADD

Line 26@{UB} assumes that all the directories mentioned in the command line
are on the boot disk. If one of them is @{B}not@{UB}, then those @{B}after@{UB} the
non-existing one will @{B}not@{UB} be added.

@{B}27. IF EXISTS Sys:Tools
28.   PATH Sys:Tools ADD
29.   IF EXISTS Sys:Tools/Commodities
30.     PATH Sys:Tools/Commodities ADD
31.   ENDIF
32. ENDIF

Lines 27-32@{UB} are set up this way as a floppy based system will have the
"Tools" directory on the Extras disk whereas a hard drive based system
will normally have "Tools" on the boot disk. This use of "IF EXISTS" allows
a common startup-sequence for both floppy disk and hard drive based
systems.

@{B}33. IF ${sys/keyboard} NOT EQ "*${sys/keyboard}"
34.   SETMAP ${sys/keyboard}
35.   ELSE
36.   PICKMAP Sys: initial
37. ENDIF

Lines 33-37 check to see if a keymap has previously been picked. If not,
then the script "PICKMAP" (in the "S" directory) is used to let the user
choose one. The way it is done seems, to me, to be to be far too complex.
(See below for my alternative.)

@{B}Line 33@{UB} is the key to this. If an environmental variable called "keyboard"
exists in Env:Sys/ then its value is substituted for the "${sys/keyboard}"
that is immediately after the "if". For example, if "keyboard" held the
value "usa" the line would become:-

   IF usa NOT EQ ....

If there is NO environmental variable then the line is not interpreted
into a variable value but is taken literally as:-

  IF ${sys/keyboard} NOT EQ ...

and the comparison of equality with the part after the NOT EQ is done
on a character by character basis starting with the "$"

In the last part of the line, "*${sys/keyboard}", the "*" has the effect
of negating the interpretation of the "$" as an indicator of a variable.
However, in assigning a value to the string, the "*" is ignored. Thus,
no matter whether or not there is a variable called "keyboard", this is
evaluated as a literal string, NOT as a variable. It becomes:-

   .... NOT EQ ${sys/keyboard}

and the comparison of equality with the part before the NOT EQ is done
on a character by character basis starting with the "$"

Now, if there IS a keyboard variable holding, say, "usa" then Line 33
literally becomes:-

  IF usa NOT EQ ${sys/keyboard}

which is TRUE (the two sides are unequal) so Line 34 is executed as:-

  SETMAP usa

If there is NO variable named "keyboard", then Line 33 literally becomes:-

  IF ${sys/keyboard} NOT EQ "${sys/keyboard}

which is UNTRUE (the two sides are identical) and Line 36 is executed.

As I said above, Line 33, although a clever piece of programming, is far
too complex a way of finding out whether the file "Env:Sys/Keyboard" exists
or not. I cannot see why they did not simply use:-

    33. IF EXISTS Env:Sys/Keyboard
    34.   SETMAP ${sys/keyboard}
    35. ELSE
    36.   PICKMAP Sys: initial
    37. ENDIF

@{B}38. IF EXISTS S:User-Startup
39.   EXECUTE S:User-Startup
40. ENDIF

Lines 38-40@{UB} check for a User-Startup file. This replaces the StartupII
file used in @{B}v1.3.@{UB} The idea is that you leave the standard Startup-Sequence
file unaltered and put all your customisations into your User-Startup file.

@{B}41. LOADWB

42. ENDCLI >Nil:@{UB}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "2.1" "Part 71 - Startup-Sequence Files - The 2.1 Startup-Sequence File"

@{B}@{U}The V2.1 Startup-Sequence File@{UB}@{UU}

@{B} 0. ; $VER: startup-sequence 38.22 (24.4.92)

Line 0@{UB} makes use of the @{"version string" LINK "ADB:Parts_51-60/54.Version/String"} (starting with $VER:) to enable
version to be checked with the VERSION command.

@{B} 1. C:SETPATCH QUIET
 2. VERSION >Nil:

Line 2@{UB} checks to ensure that the disk is being booted on an Amiga with
kickstart @{B}v2.04 or higher@{UB}. The VERSION command also sets the shell's local
variables $Workbench and $Kickstart which are used in @{"lines 26-27" LINK "2.1"113}. (See
article @{"VERSION" LINK "ADB:Parts_51-60/54.Version/Finding"23}.)

@{B} 3. ADDBUFFERS >Nil: df0: 15
 4. FAILAT 21
 5.

 6. MAKEDIR Ram:T Ram:Clipboards Ram:Env Ram:Env/Sys

Line 6@{UB} sets up all the RAM: directories that the system will use.

@{B} 7. COPY >Nil: Envarc: Ram:Env ALL NOREQ@{UB}

@{UB}Line 7@{UB} was a new addition to startup-sequence files from @{B}v2.0 onwards@{UB}.
It copies the contents of Envarc: (which is automatically assigned to
Sys:Prefs/Envarc on boot up) to Ram:Env to allow faster access to your
personal preference settings which are held in Sys:Prefs/Envarc. Programs
will look in Ram:Env for preference settings. If you save new settings
from a preferences program (e.g. printer) then the new setting will be
saved to both Sys:Prefs/Envarc AND Ram:Env.

@{B} 8. ASSIGN >Nil: Env: Ram:Env

Line 8@{UB} makes available the assigned Env: directory for environmental
variables that were first introduced in @{B}v1.3.@{UB} (Ram:Env is created in
Line 6.)

@{B} 9. ASSIGN >Nil: T: Ram:T

Line 9@{UB} sets up the assigned T: directory for the use of scripts. (Ram:T is
created in Line 6.)

@{B}10. ASSIGN >Nil: Clips: Ram:Clipboards

Lines 10@{UB} makes available the assigned directory CLIPS:. This is used by
the Clipboard.device to store clips. (Ram:Clipboards is created in Line 6.)

@{B}11. ASSIGN >Nil: Rexx: S:

Line 11@{UB} allows use of an assigned directory Rexx: for use with ARexx scripts.
You can change this to another directory if you like, For example, you
could create a directory called Sys:ARexxS and assign Rexx: to this if
you do not want your ARexx scripts to be mixed in with other scripts.

@{B}12. ASSIGN >Nil: Printers: Devs:Printers
13. ASSIGN >Nil: Keymaps: Devs:Keymaps
14. ASSIGN >Nil: Locale: Sys:Locale

Lines 12-14@{UB} are a new set of assigned directories to handle the way @{B}v2.1@{UB}
works.

@{B}15. IF NOT EXISTS Sys:Fonts
16.   ASSIGN Fonts:
17. ENDIF

Lines 15-17@{UB} were not in @{B}v2.0.@{UB} The @{B}v2.0@{UB} floppy workbench disk had an empty
Fonts directory so the automatic assignment of Fonts: was made to this.
In @{B}v2.1,@{UB} there is no Fonts directory on the floppy workbench disk so the
automatic assignment of Fonts: is made to the root directory of the boot
disk. Hard drive systems should have a Fonts: directory on their boot
partition so the automatic assignment of Fonts: is made to Sys:Fonts.
These three lines check to see if there is a Fonts directory on the boot
disk. If not, then the assignment of Fonts: is removed by line 16 so that
the system will call for the disk named "Fonts:" to be inserted when fonts
are needed rather than trying to find non existent fonts.

@{B}18. BINDDRIVERS

19. MOUNT >Nil: Devs:DOSDrivers/~(#?.info)

Line 19@{UB} is new to @{B}v2.1@{UB} but replaces the MOUNT commands in @{"lines 23-25" LINK "2.0"100}
in @{B}v2.0.@{UB} It takes advantage of the new (to @{B}v2.1@{UB}) concept of DOSDrivers.
Every DOSDriver file in Devs:DOSDrivers directory is mounted by this command.
The "~(#?.info)" means to apply the MOUNT command on every file in the
directory except those ending in ".info". Thus the MOUNT command will not
try to operate an icon file!

@{B}20. RESIDENT >Nil: C:Execute PURE

21. IF EXISTS Devs:Monitors
22.   LIST >Nil: Devs:Monitors/~(#?.info) TO T:M LFORMAT "Devs:Monitors/%s"
23.   EXECUTE T:M
24.   DELETE >Nil: T:M
25. ENDIF

Lines 21-25@{UB} replace @{"lines 12-16" LINK "2.0"54} in @{B}v2.0. V2.0@{UB} uses the "ADDMONITOR" command.
@{B}V2.1@{UB} does not have an "ADDMONITOR" command. Instead, each monitor has
its own executable driver program which is held either in "Devs:Monitors"
if it is needed or in "Extras:Storage/Monitors" if it is not needed. Line
22 puts into the file T:M a line consisting of "Devs:Monitorname" for
each monitor driver in the "Devs:Monitors" directory (the "%S" in the
@{"LFORMAT string" LINK "ADB:Parts_11-20/13.List/LFormat"} tells LIST to give only the file name and none of the other
information that LIST usually supplies). If your Devs:Monitors drawer
held one driver only called "A2024" then T:M would have one line:-

   Devs:Monitors/A2024

@{B}Line 23@{UB} can then execute each of these drivers in turn (normally only one
line for one monitor driver would be used).

@{B}26. SETENV Workbench $Workbench
27. SETENV Kickstart $Kickstart

Lines 26-27@{UB} set the global environmental variables Workbench and Kickstart
from the shell's local variables $Workbench and $Kickstart. These contain
the version numbers and thus they are made global for use by any program.

@{B}28. UNSET Workbench
29. UNSET Kickstart

Lines 28-29@{UB} are new to @{B}v2.1.@{UB} They get rid of the two local variables which
are no longer needed. In @{B}V2.0@{UB} these local variables were left in place as
the above two lines were not used.

@{B}30. IPREFS

Line 30@{UB}. @{"IPREFS" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/IPrefs"} is a program that reads the individual preference settings
from the files stored in Ram:Env/Sys (copied there from Sys:Prefs/Envarc
in Line 7) and passes them to Workbench.

@{B}31. ECHO "Amiga Release 2.1. Kickstart $Kickstart, Workbench $Workbench"

Line 31@{UB} displays the version numbers on screen.

@{B}32. CONCLIP

Line 32@{UB}. @{"CONCLIP" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/ConClip"}  is a program introduced in @{B}v2.0@{UB} that allows text to be
copied and pasted from one console window to another or to and from any
application that supports reading and writing text with the
clipboard.device.

@{B}33. PATH >Nil: Ram: C: Sys:Utilities Sys:Rexxc Sys:System S: Sys:Prefs
    Sys:WBStartup Sys:Tools Sys:Tools/Commodities

Line 33@{UB} assumes that all the directories mentioned in the command line
are on the boot disk. If one of them is @{B}not@{UB}, then those @{B}after@{UB} the
non-existing one will @{B}not@{UB} be added.

@{B}34. IF EXISTS S:User-Startup
35.   EXECUTE S:User-Startup
37. ENDIF

Lines 34-37@{UB} check for a User-Startup file. This replaces the StartupII
file used in @{B}v1.3.@{UB} The idea is that you leave the standard Startup-Sequence
file unaltered and put all your customisations into your User-Startup file.

@{B}38. RESIDENT Execute REMOVE

Line 38@{UB} was not in the @{B}V2.0@{UB} startup-sequence file. Presumably it is here
to conserve memory. Remove this line or place a semi colon in front of it
if you wish to leave EXECUTE resident.

@{B}39. LOADWB
40. ENDCLI >Nil:@{UB}


The @{B}v2.0@{UB} @{"lines 33-37" LINK "2.0"124} have gone from the startup-sequence as keymaps are
now selectable from the "Input" preferences editor.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "3.1" "Part 71 - Startup-Sequence Files - The 3.1 Startup-Sequence File"

@{B}@{U}The V3.1 Startup-Sequence File@{UB}@{UU}

@{B} 0. ; $VER: Startup-Sequence_LD 40.3 (31.8.93)
    ; Startup-Sequence for low-density floppies

Line 0@{UB} makes use of the @{"version string" LINK "ADB:Parts_51-60/54.Version/String"} (starting with $VER:) to enable
version to be checked with the VERSION command.

@{B} 1. C:SetPatch QUIET
 2. C:Version >NIL:

Line 2@{UB} checks to ensure that the disk is being booted on an Amiga with
kickstart @{B}v2.04 or higher@{UB}. The VERSION command also sets the shell's local
variables $Workbench and $Kickstart which are used in @{"Lines 35-36" LINK "3.1"154}. (See
article @{"VERSION" LINK "ADB:Parts_51-60/54.Version/Finding"23}.) Note that, even though we are now up to Version 3.1,
the VERSION command still checks to see if it is @{B}v2.04 or higher@{UB}.

@{B} 3. C:AddBuffers >NIL: DF0: 15
 4. FailAt 21

 5. C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys

Line 5@{UB} sets up all the RAM: directories that the system will use.

@{B}6. C:Copy >NIL: ENVARC: RAM:ENV ALL NOREQ@{UB}

@{UB}Line 6@{UB} was a new addition to startup-sequence files from @{B}v2.0 onwards@{UB}.
It copies the contents of Envarc: (which is automatically assigned to
Sys:Prefs/Envarc on boot up) to Ram:Env to allow faster access to your
personal preference settings which are held in Sys:Prefs/Envarc. Programs
will look in Ram:Env for preference settings. If you save new settings
from a preferences program (e.g. printer) then the new setting will be
saved to both Sys:Prefs/Envarc AND Ram:Env.

@{B} 7. Resident >NIL: C:Assign PURE

 8. Resident >NIL: C:Execute PURE

 9. Assign >NIL: ENV: RAM:ENV

Line 9@{UB} makes available the assigned Env: directory for environmental
variables that were first introduced in @{B}v1.3.@{UB} (Ram:Env is created in
Line 6.)

@{B}10. Assign >NIL: T: RAM:T

Line 10@{UB} sets up the assigned T: directory for the use of scripts. (Ram:T
is created in Line 6.)

@{B}11. Assign >NIL: CLIPS: RAM:Clipboards

Lines 11@{UB} makes available the assigned directory CLIPS:. This is used by
the Clipboard.device to store clips. (Ram:Clipboards is created in Line 6.)

@{B}12. Assign >NIL: REXX: S:

Line 12@{UB} allows use of an assigned directory Rexx: for use with ARexx
scripts. You can change this to another directory if you like, For example,
you could create a directory called Sys:ARexxS and assign Rexx: to this if
you do not want your ARexx scripts to be mixed in with other scripts.

@{B}13. Assign >NIL: PRINTERS: DEVS:Printers
14. Assign >NIL: KEYMAPS: DEVS:Keymaps
15. Assign >NIL: LOCALE: SYS:Locale

Lines 13-15@{UB} are a set of assigned directories introduced in @{B}v2.1@{UB} to handle
the way @{B}v2.1 and later@{UB} works.

@{B}16. Assign >NIL: LIBS: SYS:Classes ADD
17. Assign >NIL: HELP: LOCALE:Help DEFER

@{B}Lines 16-17@{UB} handle assigned directories new to @{B}v3@{UB}.

@{B}18. IF NOT EXISTS SYS:L
19.   Assign >NIL: L: Extras3.1:L DEFER
20. ENDIF

Lines 18-20@{UB}. Although the @{"DEFER" LINK "ADB:Parts_11-20/20.Assign/Defer"} option for ASSIGN was introduced in @{B}v2.0@{UB}
it was not until @{B}v3@{UB} that its use was introduced into the startup-sequence
file. Although there is an "L" directory on the @{B}v3.1 Workbench@{UB} disk, there
is also one on the @{B}v3.1 Extras@{UB} disk that holds all that the Workbench:L
directory has plus a lot more. Presumably, the above programming is in case
anyone wishes to do away with the "L" directory on the Workbench disk. Of
course, if you have a hard drive system, all this does not matter.

@{B}21. IF NOT EXISTS SYS:Fonts
22.   Assign FONTS:
23. EndIF

Lines 21-23@{UB} were not in @{B}v2.0.@{UB} The @{B}v2.0@{UB} floppy workbench disk had an empty
Fonts directory so the automatic assignment of Fonts: was made to this.
In @{B}v2.1 and later,@{UB} there is no Fonts directory on the floppy workbench
disk so the automatic assignment of Fonts: is made to the root directory
of the boot disk. Hard drive systems should have a Fonts: directory on
their boot partition so the automatic assignment of Fonts: is made to
Sys:Fonts. These three lines check to see if there is a Fonts directory
on the boot disk. If not, then the assignment of Fonts: is removed by
line 22 so that the system will call for the disk named "Fonts:" to be
inserted when fonts are needed rather than trying to find non existent
fonts.

@{B}24. BindDrivers

25. C:Mount >NIL: DEVS:DOSDrivers/~(#?.info)

Line 25@{UB} was introduced in @{B}v2.1@{UB}. It replaces the MOUNT commands in
@{"lines 23-25" LINK "2.0"100} in @{B}v2.0.@{UB} It takes advantage of the new (to @{B}v2.1@{UB}) concept of
DOSDrivers. Every DOSDriver file in Devs:DOSDrivers directory is mounted by
this command. The "~(#?.info)" means to apply the MOUNT command on every
file in the directory except those ending in ".info". Thus the MOUNT
command will not try to operate an icon file!

@{B}26. IF EXISTS DEVS:Monitors
27.   IF EXISTS DEVS:Monitors/VGAOnly
28.     DEVS:Monitors/VGAOnly
29.   EndIF

Lines26-29@{UB} are new to @{B}v3@{UB} (as is the "VGAOnly" driver). Presumably, there
is some reason that the "VGAOnly monitor driver cannot be included in the
script file created in Line 30 which @{B}excludes@{UB} the "VGAOnly" program.
Most VGA-only monitors can display the modes produced by the DBL type
monitor drivers but, if a particular VGA-only monitor cannot, then, as a
last resort, you can use the VGAOnly driver. It modifies the scan rate
slightly but does cause the left edge of the screen of flash when
horizontal scrolling is being used.

@{B}30.   C:List >NIL: DEVS:Monitors/~(#?.info|VGAOnly) TO T:M LFORMAT
                 "DEVS:Monitors/%s"
31.   Execute T:M
32.   C:Delete >NIL: T:M
33. EndIF

Lines 30-33@{UB} replace @{"lines 12-16" LINK "2.0"54} in @{B}v2.0. V2.0@{UB} uses the "ADDMONITOR" command.
@{B}V2.1@{UB} does not have an "ADDMONITOR" command. Instead, each monitor has
its own executable driver program which is held either in "Devs:Monitors"
if it is needed or in "Extras:Storage/Monitors" if it is not needed. Line
30 puts into the file T:M a line consisting of "Devs:Monitorname" for
each monitor driver in the "Devs:Monitors" directory (the "%S" in the
@{"LFORMAT string" LINK "ADB:Parts_11-20/13.List/LFormat"} tells LIST to give only the file name and none of the other
information that LIST usually supplies). If your Devs:Monitors drawer
held one driver only called "A2024" then T:M would have one line:-

   Devs:Monitors/A2024

@{B}Line 31@{UB} can then execute each of these drivers in turn (normally only one
line for one monitor driver would be used).

@{B}34. SetEnv Language "English"

Line 34@{UB} is new to @{B}v3@{UB}. It sets the initial language as "English" but this
will be changed if running "IPREFS" @{"(Line 42)" LINK "3.1"210} determines that another
language has been set up in preferences.

@{B}35. SetEnv Workbench $Workbench
36. SetEnv Kickstart $Kickstart

Lines 34-36@{UB} set the global environmental variables Workbench and Kickstart
from the shell's local variables $Workbench and $Kickstart (which were
created by the VERSION command in Line 2. These contain the version numbers
and thus they are made global for use by any program.

@{B}37. UnSet Workbench
38. UnSet Kickstart

Lines 37-38@{UB} (which were not in @{B}v2.0@{UB}). They get rid of the two local
variables which are no longer needed. In @{B}V2.0@{UB} these local variables were
left in place as the above two lines were not used.

@{B}39. C:AddDataTypes REFRESH QUIET

Line 39@{UB} is new to @{B}v3@{UB}. It uses @{"ADDDATATYPES" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/AddDataTypes"} to add the datatype files which
programs such as MULTIVIEW use to identify the type of file which they are
attempting to read. The @{"REFRESH" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/AddDataTypes"31} option causes ADDDATATYPES to scan the
"DEVS:Datatypes" directory for datatype descriptor files.

@{B}40. IF "`C:Avail TOTAL`" GE "1250000" VAL

Lines 40-50@{UB} are new to @{B}v3@{UB} and are used only on floppy drive systems. You
should not find them in a hard drive startup-sequence file.

@{B}Line 40@{UB} uses the @{"principle" LINK "ADB:Parts_01-10/04.Using_Shell-CLI/Another"} that enclosing commands within back apostrophes
`` causes the @{B}result@{UB} of the command to be inserted into the command line.
Thus, if your total available memory produced by the command:-

  C:Avail TOTAL

was equal to, for example, 2,250,000bytes (i.e. more than 1,250,000 bytes)
then Line 40 would become:-

  IF "2250000" GE "1250000" VAL

and, as this would be @{B}true@{UB}, then everything from Line 41 to 47 would be
carried out. These lines place a lot of disk based files into memory so
that the Workbench floppy disk is not needed as much and the disk drive
can be used for other disks. Also, accessing these files from memory rather
than from disk would be a lot faster. A hard drive system would not need
such a setup as the hard disk is never removed and access to it is a lot
faster than to a floppy.

If available system memory was less than 1,250,000 then Lines 41-47 would
be bypassed and Line 49 executed.

@{B}41.   C:LoadResource LIBS:diskfont.library LIBS:iffparse.library
            LIBS:asl.library LIBS:commodities.library

Line 41@{UB} uses the @{"LOADRESOURCE" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/LoadResource"} command to make the various libraries
resident and thus the floppy workbench disk will not be needed when they
are called.

@{B}42.   C:IPrefs

43.   IF NOT $Language EQ "English"
44.     CD LOCALE:Catalogs/$Language
45.     C:LoadResource Sys/workbench.catalog Sys/libs.catalog
           Sys/prefs.catalog Sys/commodities.catalog Sys/utilities.catalog
46.     CD SYS:

Lines43-46@{UB} are used if the @{"IPREFS" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/IPrefs"} program (Line 42) finds the "English"
is @{B}not@{UB} the preferred language. Line 45 makes the various  non-English
catalogs resident.

@{B}47.   EndIF

48. Else
49.   C:IPrefs
50. EndIF@{UB}

Line 49@{UB} is only reached if Lines 41-47 are not executed, i.e. if available
memory is was less than 1,250,000 as determined by line 40.

@{B}In a Hard Drive System@{UB} Lines 40-50 are replaced by just one line:-

  C:IPrefs

@{"IPREFS" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/IPrefs"} is a program that reads the individual preference settings from
the files stored in Ram:Env/Sys (copied there from Sys:Prefs/Envarc in
Line 7) and passes them to Workbench.

@{B}51. C:ConClip

Line 51@{UB}. @{"CONCLIP" LINK "ADB:Parts_61-70/65.Misc_Startup_Commands/ConClip"}  is a program introduced in @{B}v2.0@{UB} that allows text to be
copied and pasted from one console window to another or to and from any
application that supports reading and writing text with the
clipboard.device.

@{B}52. Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs
        SYS:WBStartup SYS:Tools SYS:Tools/Commodities

Line 52@{UB} assumes that all the directories mentioned in the command line
are on the boot disk. If one of them is @{B}not@{UB}, then those @{B}after@{UB} the
non-existing one will @{B}not@{UB} be added.

@{B}53. IF EXISTS S:User-Startup
54.   Execute S:User-Startup
55. EndIF

Lines 52-55@{UB} check for a User-Startup file. This replaces the StartupII
file used in @{B}v1.3.@{UB} The idea is that you leave the standard Startup-Sequence
file unaltered and put all your customisations into your User-Startup file.

@{B}56. Resident Execute REMOVE
57. Resident Assign REMOVE

Lines 56-57@{UB} are presumably here to conserve memory. Remove these lines or
place a semi colon in front of them if you wish to leave EXECUTE and ASSIGN
resident.

@{B}58. C:LoadWB
59. EndCLI >NIL:@{UB}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE


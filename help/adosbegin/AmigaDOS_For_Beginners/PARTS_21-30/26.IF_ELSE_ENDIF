@DATABASE "ADB26"
@INDEX "ADB:Misc/Index.Text/Main"
@HELP "ADB:Misc/Help/Main"
@NODE "Main" "AmigaDOS For Beginners - Part 26 - IF  ELSE  ENDIF"

@{B}@{U}@{JCENTER}AMIGADOS FOR BEGINNERS

BY FRANK BUNTON

@{" COPYRIGHT © FRANK P. BUNTON 1993-1998 " LINK "ADB:Misc/Read_Me_First!!/Copyright"}

@{B}@{U}PART 26 - THE "IF" "ELSE" "ENDIF" COMMANDS@{UB}@{UU}
@{JLEFT}
@{B}Location@{UB}     - v2+  - Internal
               v1.x - "c" directory

@{B}Purpose@{UB}      - To test for conditions and carry out one action or another.

@{B}Availability@{UB} - All AmigaDOS Versions
@{JCENTER}

@{" General Comments " LINK "General"} All AmigaDOS Versions
@{" The Condition    " LINK "Condition"} All AmigaDOS Versions
@{" The Action       " LINK "Action"} All AmigaDOS Versions
@{" Nested IFs       " LINK "Nested"} All AmigaDOS Versions

@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "General" "Part 26 - IF  ELSE  ENDIF - General comments"

@{B}@{U}General Comments@{UB}@{UU}

The commands:-

  IF   ELSE   ENDIF

will only work within a "script" or "command" file. You cannot use them
in a CLI/Shell window.

Do you have young kids? Or are you young enough to remember your own
childhood? How often has a parent said to a child something like:-

   @{B}IF@{UB}   you are good you can watch T.V. or
   @{B}ELSE@{UB} you go straight to bed."

The computer version of "IF" is much the same.

There are two formats for using IF. The first is:-

  IF Condition
     Action
  ENDIF
  ; rest of file

The @{"Condition" LINK "Condition"} and @{"Action" LINK "Action"} are more fully discussed later.

If "Condition" is TRUE  then "Action" is executed.

If "Condition" is FALSE then "Action" is ignored.

In both cases the "rest of file" is reached.

The other format is:-

  IF Condition
     Action 1
  ELSE
     Action 2
  ENDIF
  ; rest of file

If "Condition" is TRUE then:-

   "Action 1" is executed and
   "Action 2" is ignored.

If "Condition" is FALSE then:-

   "Action 1" is ignored  and
   "Action 2" is executed

It is NOT possible for both actions to be executed.

In both cases "rest of file" is reached.

Translating the parent's ultimatum to the children, this would read:-

  IF you are good
     Watch T.V.
  ELSE
     Go to bed
  ENDIF

So let's put this into a script file (not the T.V. and bed!) as something
that can be demonstrated. Have a look at Example_26.1:-

  ; Example-26.1

  IF EXISTS df0:s/startup-sequence
    ECHO "Disk has a startup-sequence file"
  ELSE
    ECHO "NO startup-sequence file on disk"
  ENDIF

Execute this script by entering:-

  > CD ADB:Parts_21-30
  > Example-26.1

(By the way, from now on I will not tell you to do the above two lines with
example scripts. I will assume that you will remember to do it each time!)

IF the file being tested @{B}does EXIST@{UB} in the nominated file then the first
ECHO (before the ELSE) is executed.

IF the file being tested does NOT EXIST in the nominated file then the
second ECHO (after the ELSE) is executed.

Try it out on a few disks some of which have a startup-sequence and some
of which do not so that you can make sure that it works properly.

If you do not have a hard drive and you want to put another disk in df0:
to test this out, then make the following commands @{"resident" LINK "ADB:Parts_21-30/24.Resident/Main"} before removing
your workbench disk:-

  - @{B}v1.x users@{UB}  ECHO  IF  ELSE  ENDIF
  - @{B}v2+  users@{UB}  ECHO (the others are internal commands)


@{JCENTER}=== End of Text ===
@{JLEFT}







@ENDNODE

@NODE "Condition" "Part 26 - IF  ELSE  ENDIF - The Condition"

@{B}@{U}THE "CONDITION"@{UB}@{UU}

The "Condition" following the IF can contain a number of keywords. The
EXISTS @{"shown earlier" LINK "General"64} is just one of them. Each one of these keywords tests
for a certain level of "Return Code" which we will cover more fully in
the article @{"FAIL AT & RETURN CODES" LINK "ADB:Parts_21-30/29.FailAt_&_Return_Code/RC"}

They are:-

  @{B}EXISTS@{UB} - tests for the existence of a file, directory or device

  @{B}WARN@{UB}   - tests for a return code greater or equal to 5

  @{B}ERROR@{UB}  - tests for a return code greater or equal to 10

  @{B}FAIL@{UB}   - tests for a return code greater or equal to 20

           (ERROR and FAIL only work if the command @{"FAILAT" LINK "ADB:Parts_21-30/29.FailAt_&_Return_Code/FailAt"}
           has previously been used.

  @{B}NOT@{UB}    - Reverses the interpretation of the result

  @{B}EQ GT GE@{UB} "Equal", "Greater Than" and "Greater Than or Equal"

@{B}EXISTS@{UB} has already been discussed with @{"Example-26.1" LINK "General"64}  

@{B}WARN@{UB} will be discussed when we cover the @{"ASK" LINK "ADB:Parts_21-30/27.Ask/MAIN"57} command.

@{B}ERROR and FAIL@{UB} will be discussed with @{"return codes" LINK "ADB:Parts_21-30/29.FailAt_&_Return_Code/RC"17}.)

@{B}EQ GT GE@{UB} will not really be of use until we look at @{"environmental variables" LINK "ADB:parts_31-40/40.Env-Variables/Main"}.
but @{"click here" LINK "EQGTGE"} for a brief discussion.

That leaves @{B}NOT@{UB} to dicuss here.

We have already seen how EXISTS can work by testing for the existence of a
file. Let's see how NOT works by reversing the @{"previous example" LINK "General"64} so that it
becomes:-

  ; Example-26.2

  IF NOT EXISTS df0:s/startup-sequence
    ECHO "NO startup-sequence file on disk"
  ELSE
    ECHO "Disk has a startup-sequence file"
  ENDIF

This gives exactly the same result as the "Example-26.1". You will find
that there will be times when it is better to use one method and times
when the other method is preferable.


@{JCENTER}=== End of Text ===
@{JLEFT}


























@ENDNODE

@NODE "Action" "Part 26 - IF  ELSE  ENDIF - The Action"

@{B}@{U}THE "ACTION"@{UB}@{UU}

In previous examples @{"26.1" LINK "General"64} and @{"26.2" LINK "Condition"36}, the action to be executed has consisted
of only one AmigaDOS command. In actual fact, you can have as many command
lines as you like. For example:

  IF Condition
     Action 1
     Action 2
     Action 3
  ELSE
     Action 4
     Action 5
     Action 6
   ENDIF

Just remember that you must start with an IF and end with an ENDIF.

The ELSE and its action(s) are optional.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Nested" "Part 26 - IF  ELSE  ENDIF - Nested IFs"

@{B}@{U}NESTED IFs@{UB}@{UU}

You can "nest" a number of IFs within each other. For example:-

  IF Condition 1
      Action 1
      IF Condition 2
          Action 2
      ENDIF
  ELSE
      Action 3
  ENDIF

In this case, the actions take place as follows:-

 @{B} Conditions@{UB}    @{B}@{U}Action 1@{UB}@{UU} @{B}@{U}Action@{UB}@{UU} @{B}@{U}2 Action 3@{UB}@{UU}
  @{B}@{U} 1   @{UB}@{UU}  @{B}@{U} 2   @{UB}@{UU}
  True   True     Yes      Yes      No
  True   False    Yes      No       No
  False  True     No       No       Yes
  False  False    No       No       Yes

Note that "Action 2" can only take place if BOTH conditions are true.
If "Condition 1" is false, then the system searches forward for the correct
ELSE or, if there is no ELSE, for the correct ENDIF. The nested
"IF Condition 2" would never be tested.

In a nest, each IF must have its own ENDIF. The last ENDIF belongs to
the first IF. The centre most ENDIF belongs to the centre most IF.

An example of a nested IF is given in @{"Example27-1" LINK "ADB:Parts_21-30/27.Ask/Main"57} which is contained in
the next article.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "EQGTGE" "Part 26 - IF  ELSE  ENDIF - EQ  GT  GE"

@{B}@{U}The  EQ  GT  GE  Keywords@{UB}@{UU}

The three keywords EQ GT GE used in conjunction with IF have these
meanings:-

  EQ   Equal to
  GT   Greater Than
  GE   Greater Than or Equal

It is a bit difficult to explain them properly until we have looked at
@{"environmental variables" LINK "ADB:parts_31-40/40.Env-Variables/Main"} but this script should give you an idea of how they
work:-

  ; Example-26.3

  IF 1 EQ 1
    ECHO "1 is equal to 1"
  ELSE
    ECHO "1 is NOT equal to 1"
  ENDIF

  IF 2 GT 1
    ECHO "2 is greater than 1"
  ELSE
    ECHO "2 is NOT greater than 1"
  ENDIF

  IF 2 GT 2
    ECHO "2 is greater than 2"
  ELSE
    ECHO "2 is NOT greater than 2"
  ENDIF

  IF 2 GE 2
    ECHO "2 is equal or greater than 2"
  ELSE
    ECHO "2 is NOT equal or greater than 2"
  ENDIF 

Output from this will be:-

  1 is equal to 1
  2 is greater than 1
  2 is NOT greater than 2
  2 is equal or greater than 2

I am sure that you can follow the logic in this program to arrive at the
output shown.

Of course, using numbers like this is really useless. But these keywords
will be seen to be of great use with @{"environmental variables" LINK "ADB:parts_31-40/40.Env-Variables/Main"} whose values
may be unknown or variable. We will also see that the comparisons can be
made on text as well as numbers.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE


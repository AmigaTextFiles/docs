@DATABASE "ARB25"
@INDEX "ARB:Misc/Index.Text/Main"
@HELP "ARB:Misc/Help/Main"
@NODE "Main" "ARexx For Beginners - Article 25 - The OPTIONS Instruction"

@{B}@{U}@{JCENTER}AREXX FOR BEGINNERS

ARTICLE 25 - OPTIONS

BY FRANK BUNTON@{UB}@{UU}

@{" COPYRIGHT © FRANK P. BUNTON 1995-1998 " LINK "ARB:Misc/Read_Me_First!!/Copyright"}

@{" OPTIONS General Discussion " LINK "General"}
@{"   The PROMPT  Keyword      " LINK "Prompt"}
@{"       Resetting PROMPT     " LINK "ResetPrompt"}
@{"       Warning on PROMPT    " LINK "WarningPrompt"}
@{"   The FAILAT  Keyword      " LINK "FailAt"}
@{"       Resetting FAILAT     " LINK "ResetFailAt"}
@{"   The RESULTS Keyword      " LINK "Results"}
@{"       Warning on RESULTS   " LINK "WarningResults"}
@{"   The NO      Keyword      " LINK "No"}
@{"   The CACHE   Keyword      " LINK "Cache"}
@{"   Resetting The Options    " LINK "Resetting"}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "General" "Article 25 - The OPTIONS Instruction - General"
@{JCENTER}
@{B}@{U}THE OPTIONS INSTRUCTION - GENERAL DISCUSSION@{UB}@{UU}
@{JLEFT}
The @{B}OPTIONS@{UB} instruction (along with the @{"PRAGMA()" LINK "ARB:Articles_21-30/26.Pragma/General"} function discussed in
the next article) can be used to help you control the environment in which
you are working in these ways:-

- Set the text of the prompt that is used at PULL or PARSE PULL
  instruction clauses (normally there is no prompt),

- Set the level of failure at which command clauses will be reported as
  errors,

- Instruct ARexx to request a result in the @{"RESULT" LINK "ARB:Articles_11-20/16.Functions-Internal/Result"} symbol when it sends
  commands to a host program.

OPTIONS can be used in the following ways:-

  OPTIONS PROMPT expression  See @{" The PROMPT  Keyword   " LINK "Prompt"}
  OPTIONS PROMPT

  OPTIONS FAILAT expression  See @{" The FAILAT  Keyword   " LINK "FailAt"}

  OPTIONS RESULTS            See @{" The RESULTS Keyword   " LINK "Results"}

  OPTIONS NO RESULTS         See @{" The NO      Keyword   " LINK "No"}

  OPTIONS CACHE              See @{" The CACHE   Keyword   " LINK "Cache"}

  OPTIONS                    See @{" Resetting The Options " LINK "Resetting"}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Prompt" "Article 25 - The OPTIONS Instruction - PROMPT Keyword"
@{JCENTER}
@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - "PROMPT" KEYWORD@{UB}@{UU}
@{JLEFT}
The keyword that you are most likely to use is PROMPT. This one allows
you to set a prompt string which will appear in the window when the PARSE
PULL and PULL instructions are used. Its format can be one of:-

  OPTIONS PROMPT Expression
  OPTIONS PROMPT

The "Expression" can be any string (enclosed in quotes) - for example:-

  OPTIONS PROMPT 'Enter your name '

or it can be a symbol holding a value - for example:-

  Message = 'Enter your name'
  OPTIONS PROMPT Message

The advantage of using symbols holding values will become evident when
we come to look at compound symbols and is illustrated at @{"Example30-1" LINK "ARB:Articles_21-30/30.Example_Program/Table"}.

The prompt string remains active until another prompt string is set up
or the OPTIONS setting are @{"reset" LINK "ResetPrompt"}. (Please take note of a @{"WARNING" LINK "WarningPrompt"} regarding
the use of the prompt keyword.)

So far, I have used this method of telling the user what to input:-

  SAY 'Enter your name:'
  PARSE PULL Name

This puts the string "Enter your name" on one line and then the cursor
and the input go onto the next line:-

  Enter you name:
  #

(where # represents the cursor)

But if you use OPTIONS PROMPT as in:-

  OPTIONS PROMPT 'Enter your name:'
  PARSE PULL Name

then the string "Enter your name:" appears in the window with the cursor
immediately after it instead of on the next line:-

  Enter you name:#

This is a much neater method if your prompt string is not too long.

However, it looks better if you leave a space between the end of the string
and the string delimiter so that the cursor does not appear right next
to the end of the string:-

  OPTIONS PROMPT 'Enter your name: '
  PARSE PULL Name

  --> Enter you name: #

Let's go back and have another look at Example8-2:-

  /* Example8-2 */

  SAY 'Enter your First Name:'
  PARSE PULL First
  SAY 'Enter you Surname:'
  PARSE PULL Surname
  SAY 'Enter your Age'
  PARSE PULL Age
  SAY 'Hi' First Surname'. You will be 100 in' 100 - Age 'years.'

We could rewrite this example as follows:-

  /* Example25-1 */

  OPTIONS PROMPT 'Enter your First Name: '
  PARSE PULL First
  OPTIONS PROMPT 'Enter you Surname: '
  PARSE PULL Surname
  OPTIONS PROMPT 'Enter your Age: '
  PARSE PULL Age
  SAY 'Hi' First Surname'. You will be 100 in' 100 - Age 'years.'


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "ResetPrompt" "Article 25 - The OPTIONS Instruction - Resetting PROMPT"
@{JCENTER}
@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - RESETTING THE "PROMPT"@{UB}@{UU}
@{JLEFT}
As mentioned in @{"Warning" LINK "WarningPrompt"}, it may be undesirable, in a large program, to
leave the prompt at the value to which it was previously set.

If you do @{B}NOT@{UB} want to reset it with another:-

  OPTIONS PROMPT Expression

(as we did in @{"Example25-1" LINK "Prompt"66}) then you should use one of:-

  OPTIONS PROMPT
  OPTIONS

The first is, in effect, setting the PROMPT to a null string (i.e. nothing
in the string) so that there will be no prompt at all.

The first is preferable to the second as it resets @{B}ONLY@{UB} the PROMPT option
leaving the FAILAT and RESULTS to whatever you may have set them to
elsewhere in the program.

The second, i.e. @{"OPTIONS" LINK "Resetting"} on its own, will reset @{B}ALL@{UB} the options to their
defaults which is O.K. if you have only used the PROMPT option but I would
suggest that you get into the habit of using OPTIONS PROMPT @{B}just in case!!@{UB}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "WarningPrompt" "Article 25 - The OPTIONS Instruction - Warning For PROMPT Keyword"
@{JCENTER}
@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - WARNING FOR "PROMPT" KEYWORD@{UB}@{UU}
@{JLEFT}
If you use OPTIONS PROMPT for a PARSE or PULL and then, a lot later in
the program, you use another PARSE or PULL, you @{B}must@{UB} remember that the
@{B}prompt will still be active@{UB} but may no longer be relevant. If it is not
relevant, then you must either set another prompt or @{"reset" LINK "ResetPrompt"} the PROMPT
to its default of nothing.

An example of the use of OPTIONS PROMPT to reset the prompt is given in
the second last line of @{"Example27-5" LINK "ARB:Articles_21-30/Example27-5.rexx/MAIN"19} in the next article. The following
extract from that example program, written into Example25-2, shows how
the use of OPTIONS PROMPT without resetting OPTIONS can cause
problems:-

  /* Example25-2 */

  cls = 'c'x
  cd  = 'a'x

  /* Following OPTIONS PROMPT used at another part of the program */

  OPTIONS PROMPT cd'Press Any Key to Continue'
  /* .... programming that uses options prompt */

  SAY cls'This is another part of the program'
  PULL Anything

  /* Menu section of program */

  Menu = 0
  SAY cd'Select a menu item and press return'
  SAY cd'1. Menu Item 1'
  SAY '2. Menu Item 2'
  SAY '3. Menu Item 3'
  SAY cd'Q to quit'
  PULL Menu

If you RX this Example25-2 you will get this screen display:-

  This is another part of the program

  Press Any Key to Continue

then:-

  Select a menu item and press return

  1. Menu Item 1
  2. Menu Item 2
  3. Menu Item 3

  Q to quit

  Press Any Key to Continue

The string "Press Any Key to Continue" has appeared in an unwanted place.

This problem is overcome in Example25-3:-

  /* Example25-3 */

  cls = 'c'x
  cd = 'a'x

  /* Following OPTIONS PROMPT used at another part of the program */

  OPTIONS PROMPT cd'Press Any Key to Continue'
  /* .... programming that uses options prompt */

  SAY cls'This is another part of the program'
  PULL Anything

  OPTIONS PROMPT /* reset options prompt to defaults */

  /* Menu section of program */

  Menu = 0
  SAY cd'Select a menu item and press return'
  SAY cd'1. Menu Item 1'
  SAY '2. Menu Item 2'
  SAY '3. Menu Item 3'
  SAY cd'Q to quit'
  PULL Menu

Now we get the menu displayed @{B}without@{UB} the unwanted string.

@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "FailAt" "Article 25 - The OPTIONS Instruction - FAILAT Keyword"

@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - "FAILAT" KEYWORD@{UB}@{UU}
@{JLEFT}
This is a bit beyond the scope of the beginner so skip over it if you
like and go on to the @{"RESULTS" LINK "Results"} keyword.

The FAILAT keyword allows you to tell ARexx the level of command return
codes above which an ARexx message will be given. Its format is:-

  OPTIONS FAILAT Expression

where "Expression" is an @{B}integer number@{UB} or a @{B}symbol@{UB} holding the value
of an integer number.

We have already seen an example of its use in @{"Example23-3" LINK "ARB:Articles_21-30/Example23-3.rexx/MAIN"6} which was
explained in @{"Article 23" LINK "ARB:Articles_21-30/23.Talking-Programs-1/Format2"25}.

When a @{B}command@{UB} is issued from ARexx to an @{B}external host@{UB}, the host will
return a code number to ARexx to indicate the status of the command. These
return code numbers may vary from one host program to another so you will
need to check your program manuals.

AmigaDOS will return the following code numbers:-

   0 - All O.K. - no problems

   5 - @{B}Warn@{UB} indicates some minor level of fault that can usually be
       safely ignored, or an indication that one of two possible
       conditions exists, the other condition returning zero.

  10 - @{B}Error@{UB} indicates that something did not happen that really
       should have happened and probably should be looked at.

  20 - @{B}Failure@{UB} indicates a more serious level of failure that really
       should not be ignored.

The ARexx default FAILAT number is 10 which means that ARexx will report
return code numbers of 10 or more but will not report number less than
10.

As an example of its use, try out this program:-

  /* Example25-4 */

  ADDRESS 'COMMAND'

  SAY 'a'x'First Go - FailAt is default 10'
  CALL DoIt

  OPTIONS FAILAT 0
  SAY 'a'x'Second Go - FailAt is now 0'
  CALL DoIt

  OPTIONS FAILAT 21
  SAY 'a'x'Third Go - FailAt is now 21'
  CALL DoIt

  OPTIONS FAILAT 10
  SAY 'a'x'Fourth Go - FailAt is back to default 10'
  CALL DoIt

  EXIT

  DoIt:
    'COPY > Nil: Ram:Garbage-1 Ram:Garbage-2'
    'Delete >Nil: Ram:Garbage-2'
  RETURN

@{B}First the function "DoIt".@{UB} Having used "ADDRESS COMMAND" at the start
of this program, the two lines in "DoIt" will send to AmigaDOS a "Copy"
command and a "Delete" command. Assuming that you @{B}do not@{UB} have files in
Ram: called "Garbage-1" or "Garbage-2" then both these AmigaDOS commands
will fail with return codes of:-

   Copy    - 20
   Delete  - 5

The use of ">Nil:" ensures that any output from the AmigaDOS command will
be suppressed. This will ensure that all output we see is from
ARexx.

@{B}"First Go"@{UB} does not use the FAILAT option so the default of 10 is in force.
The program will output:-

  First Go - FailAt is default 10
  COPY failed returncode 20
     23 *-* 'COPY > Nil: Ram:Garbage-1 Ram:Garbage-2';
  +++ Command returned 20

The line:-

  COPY failed returncode 20

will appear no matter what the FAILAT level is set at. It is generated
by ARexx after AmigaDOS returns control to ARexx.

The two lines:-

     23 *-* 'COPY > Nil: Ram:Garbage-1 Ram:Garbage-2';
  +++ Command returned 20

are the error messages generated by ARexx because "Copy" sent back a code
20 which is equal to or higher than the default "FailAt" of 10

Don't worry about what all this means yet. It will be explained in
@{"Articles 46" LINK "ARB:Articles_41-50/46.Error_Tracing-1/MAIN"999}. Just take note that it is these types of lines that the FAILAT
keyword can be used to suppress.

Note that there are no error messages from "Delete" as its return code
is only 5.

@{B}"Second Go"@{UB} uses "FAILAT 0" so that all ARexx messages will be displayed.
Its output is:-

  Second Go - FailAt is now 0
  COPY failed returncode 20
     23 *-* 'COPY > Nil: Ram:Garbage-1 Ram:Garbage-2';
  +++ Command returned 20
     24 *-* 'Delete Ram:Garbage-2';
  +++ Command returned 5

Now the "Delete" command will also have the ARexx error messages because
it gives a return code of 5 which is equal to or higher than the "FailAt"
level of 0 set by "OPTIONS FAILAT 0".

@{B}"Third Go"@{UB} uses "FAILAT 21" to set the limit beyond the 20 that "Copy"
returns. The output is therefore:-

  Third Go - FailAt is now 21
  COPY failed returncode 20

Now none of the ARexx error messages appear. They have all been suppressed
as both AmigaDOS commands return code numbers below the level of
21.

@{B}"Fourth Go"@{UB} uses "FAILAT 10" to @{"reset" LINK "ResetFailAt"} it to the default setting. Its
output:-

  Fourth Go - FailAt is back to default 10
  COPY failed returncode 20
     23 *-* 'COPY > Nil: Ram:Garbage-1 Ram:Garbage-2';
  +++ Command returned 20

is the same as that of "First Go".


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "ResetFailAt" "Article 25 - The OPTIONS Instruction - Resetting FAILAT Keyword"

@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - RESETTING "FAILAT" KEYWORD@{UB}@{UU}
@{JLEFT}
FAILAT will be reset to its default value of 10 when @{B}all@{UB} OPTIONS values
are reset using @{"OPTIONS" LINK "Resetting"} on its own.

However, if @{B}only@{UB} the FAILAT is to be reset, it is best to use:-

  OPTIONS FAILAT 10

as this changes @{B}only@{UB} the FAILAT value and @{B}not@{UB} any other OPTIONS values
that you may have set up.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Results" "Article 25 - The OPTIONS Instruction - RESULTS Keyword"

@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - "RESULTS" KEYWORD@{UB}@{UU}
@{JLEFT}
The RESULTS keyword tells ARexx to ask the external host program to return
a result into the @{"RESULT" LINK "ARB:Articles_11-20/16.Functions-Internal/Result"} symbol. However, for this to work, the external
program must have been written in such a way as to be able to send back
the RESULT value. Check out your program's manual.

(See also @{"Warning" LINK "WarningResults"} about the use of the RESULTS keyword.)

The following program illustrates its use:-

  /* Example25-5 */

 1.  IF SHOW('P','Ed') = 0 THEN DO
 2.    SAY 'The primary "ED" window is NOT open'
 3.    SAY 'a'x'Opening Primary "ED" window now'||'a'x
 4.    ADDRESS 'COMMAND' 'Run >Nil: Ed Dummy'
 5.  END
 6.  ELSE SAY 'The primary "ED" window IS already open'||'a'x

 7.  WAITFORPORT 'Ed'

 8.  ADDRESS 'Ed'

 9.  'sm "This is a status line message"'

10.  SAY 'Value of RESULT before "OPTIONS RESULTS =' Result

11.  OPTIONS RESULTS

12.  'sm "ANOTHER STATUS LINE MESSAGE"'

13.  SAY 'Value of RESULT after  "OPTIONS RESULTS =' Result

If @{B}Primary ED@{UB} (i.e. the "ED" window having the port name "Ed") is already
open, the output will be:-

  The primary "ED" window IS already open

  Value of RESULT before "OPTIONS RESULTS = RESULT
  Value of RESULT after  "OPTIONS RESULTS = ANOTHER STATUS LINE MESSAGE

If @{B}Primary ED@{UB} is NOT yet open, it will open and the output will be:-

  The primary "ED" window is NOT open

  Opening Primary "ED" window now

  Value of RESULT before "OPTIONS RESULTS = RESULT
  Value of RESULT after  "OPTIONS RESULTS = ANOTHER STATUS LINE MESSAGE

In @{B}both@{UB} cases, note that the symbol "Result" is:-

- @{"unitialised" LINK "ARB:Articles_01-10/07.Symbols_Intro/Unitialised"} before OPTIONS RESULTS is used
- has a value returned to it from "ED" after OPTIONS RESULTS is used.

@{B}Line 1-6@{UB} All I'm doing here is checking to see if the primary "ED" window
is open or not (port name "Ed") with the @{"SHOW()" LINK "ARB:Articles_21-30/24.Talking-Programs-2/P"} function. If not, it opens
an "ED" window. If it is, it tells you it is already open.

@{B}Line 7@{UB} uses the @{"Command Utility" LINK "ARB:Articles_01-10/03.Command_Utilities/Utilities"} WAITFORPORT that I will not be discussing
until @{"Article 40" LINK "ARB:Articles_31-40/40.Communications/WaitForPort"}. For now, just understand that the program will not proceed
beyond Line 7 until the port name "Ed" appears. If this is @{B}not@{UB} used, then
Line 8 can be reached before Line 4 has fully established the "ED" window
and the program stops with an error message "Host environment not
found".

@{B}Lines 9 & 12@{UB} send to "Ed" one of its commands "SM" which is supposed to
put a message into the "ED" window's status line. I cannot get it to work
from an ARexx program but this does not affect the purpose of this example,
which is to retrieve a value in the RESULT symbol.

So, take note that, in the above example, RESULT is:-

- unitialised @{B}before@{UB} OPTIONS RESULT is used,
- has a value returned to it by "ED" @{B}after@{UB} OPTIONS RESULTS is used.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "WarningResults" "Article 25 - The OPTIONS Instruction - Warning For RESULTS Keyword"

@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - WARNING FOR "RESULTS" KEYWORD@{UB}@{UU}
@{JLEFT}
When we discussed the @{"RESULT" LINK "ARB:Articles_11-20/16.Functions-Internal/Result"24} symbol in Article 16, I said that a call
to an internal function will cause the RESULT symbol to loose any previous
value.

This is @{B}NOT@{UB} the case when a result is returned from an external host.
If the host does not send back a value then RESULT will continue to hold
any value it previously had.

This is illustrated by the following variation to Example25-5 in which
a result is @{B}not@{UB} requested from "Virus_Checker":-

  /* Example25-6 */

  IF SHOW('P','Ed') = 0 THEN DO
    SAY 'The primary "ED" window is NOT open'
    SAY 'a'x'Opening Primary "ED" window now'||'a'x
    ADDRESS 'COMMAND' 'Run >Nil: Ed Dummy'
  END
  ELSE SAY 'The primary "ED" window IS already open'||'a'x

  WAITFORPORT 'Ed'

  Result = 'A wrong value'

  ADDRESS 'Ed'

  'sm "This is a status line message"'

  SAY 'Value of RESULT before "OPTIONS RESULTS =' Result

  OPTIONS RESULTS

  'sm "ANOTHER STATUS LINE MESSAGE"'

  SAY 'Value of RESULT after  "OPTIONS RESULTS =' Result

Example25-6 (above) is the same as Example25-5 except for the addition
of the line:-

  Result = 'A wrong value'

before the line:-

  ADDRESS 'Ed'

The last two lines of the output from this program should be:-

  Value of RESULT before "OPTIONS RESULTS = A wrong value
  Value of RESULT after  "OPTIONS RESULTS = ANOTHER STATUS LINE
MESSAGE

You can avoid this problem by using the @{"DROP" LINK "ARB:Articles_01-10/07.Symbols_Intro/Drop"} instruction, as in:-

  DROP Result

just before sending a command to an external host.

@{B}You have been warned!!@{UB}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "No" "Article 25 - The OPTIONS Instruction - NO Keyword"
@{JCENTER}
@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - "NO" KEYWORD@{UB}@{UU}
@{JLEFT}
The use of the NO keyword without any other keyword, as in:-

  OPTIONS NO

@{B}appears@{UB} to have the same result as:-

  @{"OPTIONS" LINK "Resetting"}

That is, it returns @{B}all the options@{UB} to their default state.

The ARexx manuals state:-

  "... OPTIONS ... also accepts the @{B}NO@{UB} keyword to reset a @{B}selected option@{UB}
   to its default value"

but the manuals make no mention of how to apply the NO to the selected
option.

The only keyword that I can make NO work in combination with is RESULTS
when used as follows:-

  OPTIONS NO RESULTS

This will turn off the request for a result from the host program but
will leave PROMPT and FAILAT at their previously set values.

The use of either of:-

  OPTIONS NO FAILAT
  OPTIONS NO PROMPT

returns a @{B}Keyword conflict@{UB} error message.

As mentioned earlier, the PROMPT and FAILAT values can be returned to
their default values without disturbing the values of the others by
using:-

  @{" OPTIONS PROMPT    " LINK "ResetPrompt"}

  @{" OPTIONS FAILAT 10 " LINK "ResetFailAt"}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Cache" "Article 25 - The OPTIONS Instruction - CACHE Keyword"
@{JCENTER}
@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - "CACHE" KEYWORD@{UB}@{UU}
@{JLEFT}

The usage of this keyword is:-

  OPTIONS CACHE

I cannot see any real point in using this option.

ARexx operates an internal cache, i.e. storage in memory, of some
instructions that are used repetitively. By doing this, scripts are
supposed to operate a bit faster.

The CACHE keyword turns this cache off. As a result, scripts may operate
a bit more slowly.

Beginners would be well advised to steer clear of this keyword.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Resetting" "Article 25 - The OPTIONS Instruction - Resetting"
@{JCENTER}
@{B}@{U}@{JCENTER}THE OPTIONS INSTRUCTION - RESETTING TO DEFAULTS@{UB}@{UU}
@{JLEFT}
If you use OPTIONS in its simplest format, i.e. on its own without any
keywords:-

  OPTIONS

then all items controlled by OPTIONS are returned to their default values
which are:-

  PROMPT  - None
  FAILAT  - Return Code of 10
  RESULTS - Results not requested

The use of:-

  @{"OPTIONS NO" LINK "No"}

@{B}appears@{UB} to have the same result.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE


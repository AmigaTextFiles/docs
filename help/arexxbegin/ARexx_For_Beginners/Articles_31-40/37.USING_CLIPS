@DATABASE "ARB37"
@INDEX "ARB:Misc/Index.Text/Main"
@HELP "ARB:Misc/Help/Main"
@NODE "Main" "ARexx For Beginners - Article 37 - Using Clips"

@{B}@{U}@{JCENTER}AREXX FOR BEGINNERS

ARTICLE 37 - USING CLIPS

BY FRANK BUNTON@{UB}@{UU}

@{" COPYRIGHT © FRANK P. BUNTON 1995-1998 " LINK "ARB:Misc/Read_Me_First!!/Copyright"}

@{" The Clip List             " LINK "List"}
@{" The Clip Name             " LINK "Name"}
@{" The Clip Value            " LINK "Value"}
@{" The RXSET Command Utility " LINK "Rxset"}
@{" The SETCLIP() Function    " LINK "Setclip()"}
@{" The GETCLIP() Function    " LINK "Getclip()"}
@{" The SHOW() CLIP Option    " LINK "Show()"}
@{" Example Programs          " LINK "Example"}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "List" "Article 37 - Using Clips - The Clip List"

@{B}@{U}@{JCENTER}USING CLIPS - THE CLIP LIST@{UB}@{UU}
@{JLEFT}
A lot of programs allow the storage of data in a @{B}ClipBoard.@{UB} For example,
word processors allow you to copy or cut text then paste it in again.
When it is copied or cut the text has to be stored somewhere in memory
so that it can be accessed for the pasting operation. This area of memory
is called a @{B}clipboard.@{UB}

The ARexx process maintains a @{B}Clip List@{UB} which is somewhat similar except
that items are added to the Clip List with the @{"SETCLIP()" LINK "Setclip()"} Built In Function
or the @{"RXSET" LINK "Rxset"} Command Utility both of which are explained in this
article.

The items in the Clip List are in what is called @{B}pairs.@{UB} This means that
each entry to the list consists of a pair of items which are:-

- the @{" Name  " LINK "Name"} of the clip, and
- the @{" Value " LINK "value"} of the clip.

The value assigned to a particular name can be changed at any time simply
by giving it a new value.

The contents of clip list are @{B}not program specific.@{UB} That means that @{B}any@{UB}
ARexx program can access the contents of the clip list even if the program
that set up the clips has finished operating. The list remains intact
until the ARexx process itself is shut down.


@{JCENTER}=== End of Text ===
@{JLEFT}










@ENDNODE

@NODE "Name" "Article 37 - Using Clips - The Clip Name"

@{B}@{U}@{JCENTER}USING CLIPS - THE CLIP NAME@{UB}@{UU}
@{JLEFT}
The @{B}Name@{UB} used in a Clip List entry is @{B}case sensitive@{UB}.

For example, these two names refer to @{B}different@{UB} clips:-

  TestClip
  TESTCLIP

The characters that can be included in the @{B}name@{UB} of the clip would appear
to be any that can be entered from the keyboard when using the @{"SETCLIP()" LINK "Setclip()"}
function although some characters such as ";" and "," do not appear to
be accepted when using @{"RXSET" LINK "Rxset"} as an AmigaDOS Command.

There does not appear to be any limit to the length of the name.

However, as regards both length and characters used, you would be well
advised to use the @{B}KISS Principle@{UB} (i.e. @{B}K@{UB}eep @{B}I@{UB}t @{B}S@{UB}imple, @{B}S@{UB}tupid!). Stick
to Alpha-Numeric characters and make the names as short as possible while
still maintaining some meaning and uniqueness from other names.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Value" "Article 37 - Using Clips - The Clip Value"

@{B}@{U}@{JCENTER}USING CLIPS - THE CLIP VALUE@{UB}@{UU}
@{JLEFT}
The values given to the clip @{"Name" LINK "Name"} can be numeric or string in nature.
This is the same concept as that which we looked at when talking about
assigning @{"values to symbols" LINK "ARB:Articles_01-10/07.Symbols_Intro/Assigning"}.

Examples of assigning values are given under:-

  @{" SETCLIP()       " LINK "Setclip()"}
  @{" RXSET           " LINK "Rxset"}
  @{" Example Program " LINK "Example"}


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Rxset" "Article 37 - Using Clips - RXSET Command Utility"

@{B}@{U}@{JCENTER}USING CLIPS - THE RXSET COMMAND UTILITY@{UB}@{UU}
@{JLEFT}
As with all Command Utilities, the RXSET command can be run from a Shell/CLI
window in the same way as an AmigaDOS command.

If used from an ARexx program then you @{B}must@{UB} use "ADDRESS COMMAND" first!!

It is better and easier to use @{"SETCLIP()" LINK "Setclip()"} if you are within an ARexx program.

The format for RXSET is:-

  RXSET [Name[=] [Value]]

Where @{"Name" LINK "Name"} and @{"Value" LINK "Value"} are as previously defined.

For example:-

  RXSET Address 99 Somewhere Street

The @{B}Name@{UB} and @{B}=@{UB} and @{B}Value@{UB} are optional so that you can use any of these:-

  RXSET
  RXSET Name
  RXSET Name Value
  RXSET Name= Value

You should @{B}NOT@{UB} use quotes to enclose the @{B}Name.@{UB}

If you use quotes to enclose the @{B}Value@{UB} then they can only be the double
quotes (""). Do @{B}not@{UB} use the single quotes ('') or they will be @{B}included@{UB}
in the value of the clip!

To avoid confusions, @{B}don't use any quotes!!'@{UB}

However, you @{B}can@{UB} use either apostrophes (') or inverted commas (") @{B}within@{UB}
the string.

The @{B}= sign@{UB} is also optional but, if used, it @{B}must@{UB} be @{B}immediately@{UB} after
the @{B}Name@{UB} option @{B}without@{UB} an intervening space. For example:-

  RXSET @{B}Address=@{UB} 99 Somewhere Street is @{B}correct@{UB}

  RXSET @{B}Address =@{UB} 99 Somewhere Street is @{B}WRONG!@{UB}

The @{B}wrong@{UB} method will result in a value that @{B}includes@{UB} the equal sign!!!

@{B}RXSET on its own@{UB} will give a display of all the pairs in the Clip List.
This display always has the "Name" and "Value" separated by "=" irrespective
of whether or not an "=" was used in setting up the pair.

For example:-

  RXSET Person Joe Bloggs
  RXSET Address1= 99 Somewhere Street
  RXSET Address2 = AnyTown
  RXSET

  --> Person=Joe Bloggs
      Address1=99 Somewhere Street
      Address2== AnyTown

Note that the "Address2" name has a value of "= AnyTown" as the "=" has
been incorrectly used in the RXSET command line with a space between it
and the clip name.

@{B}RXSET Name@{UB} without a value will remove that name from the clip list. However,
you @{B}must use the same case@{UB} for the @{B}name@{UB} as you used for it when setting
the value or it will @{B}not@{UB} be removed. Different case means different
clip!!


@{JCENTER}=== End of Text ===
@{JLEFT}




@ENDNODE

@NODE "Setclip()" "Article 37 - Using Clips - The SETCLIP() Function"

@{B}@{U}@{JCENTER}USING CLIPS - THE SETCLIP() FUNCTION@{UB}@{UU}
@{JLEFT}
This is the one to use when setting up clip list names from within an
ARexx program.

The format for it is:-

  SETCLIP(Name [,Value])

Where @{"Name" LINK "Name"} and @{"Value" LINK "Value"} are as previously defined.

The @{B}Name@{UB} is essential but the @{B}Value@{UB} is optional so that you can use one
of these:-

  SETCLIP(Name)
  SETCLIP(Name,Value)

If used with a "Value", the "Name Value" pair is added to the list.

If used without a "Value", the "Name" is removed from the clip list.

Examples are:-

  CALL SETCLIP('Person','Joe Bloggs')

  --> adds the pair Person=Joe Bloggs to the clip list

  CALL SETCLIP('Person')

  --> removes the pair whose name is "Person" from the clip list.

@{B}Name@{UB} and @{B}Value@{UB} can be either strings or symbols.

If a string is used (as in above examples) then enclose it in quotes.

If a symbol is used then do @{B}not@{UB} put the symbol name in quotes.

An example of using symbols instead of strings is:-

  Name = 'Person'
  Value = 'Joe Bloggs'
  CALL SETCLIP(Name,Value)


@{JCENTER}=== End of Text ===
@{JLEFT}






@ENDNODE

@NODE "Getclip()" "Article 37 - Using Clips - The GETCLIP() Function"

@{B}@{U}@{JCENTER}USING CLIPS - THE GETCLIP() FUNCTION@{UB}@{UU}
@{JLEFT}
This function retrieves the @{"value" LINK "Value"} of a clip @{"name" LINK "Name"} from the @{"clip list" LINK "List"}.

Its format is:-

  GETCLIP(Name)

Where @{"Name" LINK "Name"} is as previously defined.

For example:-

  SETCLIP('Person','Joe Bloggs')

  SAY GETCLIP('Person')

  --> Joe Bloggs

If the name does not exist then a null string is returned.


@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE

@NODE "Show()" "Article 37 - Using Clips - The SHOW() Function CLIP Option"

@{B}@{U}@{JCENTER}USING CLIPS - THE SHOW() Function CLIP Option@{UB}@{UU}
@{JLEFT}
When we looked at @{"SHOW()" LINK "ARB:Articles_21-30/24.Talking-Programs-2/Show()"} I said that we would look at the @{B}CLIP@{UB} option
when we looked at the @{"clip list" LINK "List"}.

When we use this form of SHOW():-

  SHOW('C')

we are given a list of the @{"Names" LINK "Name"} of the pairs in the clip list but @{B}not@{UB}
their @{"Values" LINK "Value"}.

For example, if the only @{"clip pairs" LINK "List"14} existing were those set in the example
given under @{"RXSET" LINK "Rxset"53} then:-

  SAY SHOW('C')
  --> Person Address1 Address2

If we use SHOW() with a pair name, as in:-

  SHOW('C',Name)

then we are given a @{"Boolean" LINK "ARB:Misc/Glossary/Boolean"} value of:-

  1 - if the name exists
  0 - if the name does not exist

If we use the "Pad" option, the names in the list will be separated by
the given pad (the space character is the default pad). If a "Pad" is
used the "Name" option must be replaced by two commas with nothing between
them. For example:-

  SAY SHOW('C',,';')
  --> Person;Address1;Address2


@{JCENTER}=== End of Text ===
@{JLEFT}







@ENDNODE

@NODE "Example" "Article 37 - Using Clips - Example Programs"

@{B}@{U}@{JCENTER}USING CLIPS - EXAMPLE PROGRAMS@{UB}@{UU}
@{JLEFT}
The following two programs demonstrate that a clip list can be set up
in one program and used in another. This is, of course, on the understanding
that the ARexx process is @{B}not@{UB} shut down between running the first program
and the second program.

  /* Example37-1 */

  CALL SETCLIP('Person.1','Joe Bloggs')
  CALL SETCLIP('Person.2','Fred Nurks')
  CALL SETCLIP('Phone.1','123 4567')
  CALL SETCLIP('Phone.2','456 7890')

  /* Example37-2 */

  DO Count = 1 to 2
    SAY 'Details of Person' Count 'are:-'
    SAY '    Name =' GETCLIP('Person.'||Count)
    SAY '   Phone =' GETCLIP('Phone.'||Count)
  END

The output of the 1st program will be @{B}nothing@{UB} in the window.

The output of the 2nd program, if run after the 1st, will be:-

  Details of Person 1 are:-
      Name = Joe Bloggs
     Phone = 123 4567
  Details of Person 2 are:-
      Name = Fred Nurks
     Phone = 456 7890

Of course, the only value of doing this sort of thing would be if you
were going to use the same set of data in a number of different ARexx
programs. You would run the program holding the data first to set up the
clip list then run one of the program that can use that data.

To demonstrate that numeric values can be held as well as strings, you
could set up a clip list of conversion factors if you are going to use
conversions in a number of programs. The following two program illustrate
this point.

  /* Example37-3 */

  SETCLIP('kmTOmile',0.6213712)
  SETCLIP('litreTOgallon',0.2199755)

  /* Example37-4 */

  SAY 'Calculate Fuel Consumption in Miles Per Gallon'||'a'x

  SAY 'Enter Number of Kilometres'
  PULL Kilometres

  SAY 'Enter Number of Litres'
  PULL Litres

  Miles = GETCLIP('kmTOmile') * Kilometres
  Gallons = GETCLIP('litreTOgallon') * Litres

  SAY 'a'x'Distance in Miles =' Miles
  SAY 'Petrol in Gallons =' Gallons
  SAY 'Miles Per Gallon =' Miles/Gallons
  SAY 'Litres Per 100km =' Litres * 100 / Kilometres

As with Examples 37-1 and 37-2, you should run Example 37-3 before
Example37-4. Also, it is not of much use having two programs instead of
all in one unless you are going to use the same conversion factors in a
number of different programs.

@{JCENTER}=== End of Text ===
@{JLEFT}
@ENDNODE
